//******************************************************************************
// YAYA　ゴーストテンプレート
// トーク辞書
//******************************************************************************


// 自動でトーク中の改行にウェイトを追加
#define \n[half] %(NewLine(1))
 // 1.5行の改行を簡単に記述できるように。2行改行をしたいときは%(NewLine(4))とかで呼ぶ。
#define \n\n %(NewLine(3))
#define \n %(NewLine(2))

// サーフェス短縮表記
#define [[ \s[
#define ]] ]

//******************************************************************************
// 特別トーク
//******************************************************************************

//------------------------------------------------------------------------------
//Init関数でスコープの設定はできているので、\1の記述は不要
//------------------------------------------------------------------------------

//------------------------------------------------------------------------------
// ボクっ子トーク
//------------------------------------------------------------------------------


SP_call_myself_boku
{
    Init
    --
    if suzu_myself == undefined{
        Boku_First
    }
    elseif suzu_myself == 'わたし'{
        Boku_StillNotChanged
    }
    else{
        Boku_AlreadyChanged
    }
}

Boku_StillNotChanged
{
    "[[1055]]%(username)、ボクっ子は好き？\n\n/
    ……[[1000]]うん、まただよ。気が変わったかなって思って。\n/
    [[1006]]あなたが望むなら、わたしはいつだって自分のことをボクって言ってあげる。\n\n/
    [[1015]]遊びたくなったらいつでも言っていいからね？？\e"
}

Boku_AlreadyChanged
{
    "[[1055]]%(username)、ボクっ子は好き？\n\n/
    [[1016]]……って、訊くまでもないか。\n/
    [[1008]]あなたは自分のことを%(suzu_myself)って呼ぶ鈴ちゃんが好きなんだものね。\n\n/
    [[1005]]いいんだよ。あなたが飽きるまでずっと続けてあげるから。\e"
}

Boku_First
{
    "[[1055]]%(username)、ボクっ子は好き？\n/
    [[1000]]あなたが望むなら、わたしは自分のことをボクって呼んでもいいんだよ。\n\n/
    [[1015]]ねえ、どうかな？%(NewLine(4, 0))/
    \q[ぜひ！！,Boku_SelectMyself]%(NewLine(2, 0))/
    \q[そのままで,Boku_StayMyselfOn]"
}

Boku_SelectMyself
{
    Init
    --
    "[[1005]]うん。\n/
    [[1020]]どの「ボク」にすればいいかな？%(NewLine(4, 0))/
    \q[ボク,Boku_ToKatakana]%(NewLine(2, 0))/
    \q[僕,Boku_ToKanji]%(NewLine(2, 0))/
    \q[ぼく,Boku_ToHiragana]%(NewLine(2, 0))/
    \q[「わたし」のまま,Boku_StayMyselfOn]"
}

Boku_ToKatakana
{
    suzu_myself = 'ボク'
    
    Boku_ChangedMyself
}

Boku_ToKanji
{
    suzu_myself = '僕'
    
    Boku_ChangedMyself
}

Boku_ToHiragana
{
    suzu_myself = 'ぼく'
    
    Boku_ChangedMyself
}

Boku_ChangedMyself
{
    Init
    --
    "[[1005]]「%(suzu_myself)」ね、わかった。\n\n/
    [[1000]]ちょっとだけあなたのために変わった%(suzu_myself)を、[[1015]]これからもよろしくね。\n/
    %(Boku_Information)"
}

Boku_StayMyselfOn
{
    suzu_myself = 'わたし'

    Init
    --
    "[[1011]]そう？\n/
    [[1000]]それじゃあ今回はそのままね。\n\n/
    [[1055]]気が向いたらいつでも言っていいからね？？\n/
    %(Boku_Information)"
}

Boku_Information
{
    "%(NewLine(2,0))/
    \_q\f[italic,1]※鈴のメニューから鈴の一人称を変えられるようになりました。\e"
}