//******************************************************************************
// マウス入力関連辞書
//******************************************************************************

// 自動でトーク中の改行にウェイトを追加
#define \n[half] %(NewLine(1))
#define \n %(NewLine(2))

// 最初のスコープ変更以外に改行とウェイトを追加
#define '\0 '\h
#define '\1 '\u
#define "\0 "\h
#define "\1 "\u
#define \0 %(NewLine(3))\0
#define \1 %(NewLine(3))\1

// サーフェス短縮表記
#define [[ \s[
#define ]] ]

//******************************************************************************
//マウス処理部
//
//以下、実際のトークを記述します。関数名は以下の規則になっています。
//[種別] + [スコープ] + [部位]
//種別は、MouseMove, MouseDoubleClick, MouseWheelUp, MouseWheelDown のどれかです。
//スコープは、\0,\1,といったスコープ番号です。
//部位は、当たり判定のどの部位かを表します。当たり判定の無い場所でのイベントの場合
//部位はありません。
//******************************************************************************


//******************************************************************************
//撫で反応
//******************************************************************************
MouseMove0Head : nonoverlap
{
    SHIORI3FW.ResetAITalkInterval

	nadeYuriHeadCount++
	skinshipYuriCount++
	fav_Yuri++

	Init
	--
	'\0[[16]]ん。\w6[[95]]恥ずかしいけど、やっぱりうれしいね。\e'

	"\0[[29]]猫扱いデスか？/
	\1[[1006]]猫じゃないの。\e"

	"\0[[6]]にゃ……/
	\1[[1058]]尻尾が揺れているわよ。/
	\0[[42]]ゆ、揺れてない！！\e"

	"\0[[55]]こういうことをすると猫も亜人も喜ぶので、みんなにもしてあげたらいいですよ。/
	\1[[1055]]主語を大きくしないで素直に自分がうれしいって言いなさい。/
	\0………[[34]]うれしいデス、ありがとうございます。\e"

	"\0[[16]]髪型、崩さないようにね……/
	\1[[1015]]もともと癖っ毛だから気にせずぐりぐりいっちゃっていいわよ。/
	\0[[67]]そこ、うるさい。\e"

	"\0[[0]]手つきが慣れていますね。他のひとにもこうしてるんですか？？/
	\1[[1060]]嫉妬してるの？/
	\0[[60]]いいえ。\w5[[16]]%(username)は優しい人なんだろうと思いを馳せているだけです。\e"

	"\0[[6]]撫で撫でコミュニケーション。\n[[16]]猫としてはうれしいけれど、人間としては子供扱いみたいで、[[56]]ちょっとフクザツ……\e"
}

MouseMove0Hand : nonoverlap
{
    SHIORI3FW.ResetAITalkInterval
	
	skinshipYuriCount++
	fav_Yuri++

	Init
	--
	"\0[[0]]肉球はないよ。[[75]]手だけ見たら、ごく普通の人間の手です。\e"

	"\1[[1050]]手も真っ白で、細くて長い指がついてて。\n[[1006]]正直ちょっと羨ましいわ。/
	\0[[60]]そんなものですかね。\n[[16]]でも、ありがと。\e"

	"\0[[10]]指輪とかつけたほうがいいですかね。\n[[4]]でも、探し方がよくわからないんですよね。\e"

	"\0[[0]]ん、人間のコミュニケーションですね。\n[[5]]いいですよ。/
	\1[[1006]]………変な言い方。\e"

	"\0[[8]]大きい手はカワイイのです。/
	\1[[1006]]その真偽は置いておいて。\n[[1050]]大きい手してるわよね。/
	\0[[35]]カワイイの、チャームポイントなの、無敵なの！！\e"

	"\0[[0]]墨のような黒のネイルだよ。\n[[16]]黒みたいにシンプルで、シャープなひとになりたいの。\e"
}

MouseMove0Ear : nonoverlap
{
    SHIORI3FW.ResetAITalkInterval
	
	harassmentYuriCount++
	fav_Yuri -= 3
	
	Init
	--
	'\0[[7]]そこは、ヤです。\e'

	"\0[[17]]%(username)。怒りますよ？\e"

	"\0[[2]]にゃ！！\n[[3]]あの、びっくりするから、やめてくださいね……\e"

	'\0[[86]]どんなにカワイくても、猫のお耳に触るのは、ダメです。[[17]]犯罪です。[[37]]禁忌です。\e'

	"\0[[37]]あなたがそうするなら私も好感度を下げて対抗しますからね？/
	\1[[1006]]焼け石に水ね。/
	\0[[35]]これは意地なの！！\e"
}

MouseMove0Tail : nonoverlap
{
    SHIORI3FW.ResetAITalkInterval
	
	Init
	--
	"\0[[4]]撫でてもいいけど、許可をとってからやさしくね。\n[[6]]敏感な場所ですから。\e"

	"\0[[55]]太くてもふもふで、とてもカワイイでしょう？\n[[26]]ちゃんと毎日ケアしてますからね。\e"

	"\0[[6]]バランスを取ったり、センサーになったり。\n[[8]]あると便利ですよ、しっぽ。\e"

	"\0[[6]]猫たちのしっぽには触らないでね。\n[[13]]嫌がる子が多いですから。\e"

	"\0[[10]]服のおしりのところに、しっぽを出す穴があるの。\n[[55]]ちゃんとそこまで考えて作られた制服なんだよ。\e"
}

MouseMove1Head : nonoverlap
{
    SHIORI3FW.ResetAITalkInterval
	
	nadeSuzuHeadCount++
	skinshipSuzuCount++
	fav_Suzu++
	
	Init
	--
	"\1[[1028]]にゃ。\w4\n[[1005]]ありがとうね、うれしいよ？\e"

	"\1[[1016]]ん。\w6[[1005]]%(username)、あいしてるよ。\e"

	"\1[[1058]]ほら、悠梨が羨ましそうに見てるわよ。[[1065]]あの子も撫でてあげなさいな。/
	\0[[39]]見てない、見てませんってば。\e"

	"\1[[1020]]髪の毛、さらさらでしょう？\n[[1005]]大事な大事なわたしの宝物なの。\e"

	"\1[[1015]]撫でられるのはすきだよ。\n[[1016]]本当にあなたの子猫になったみたいで。\e"

	"\1[[1016]]ふふ、やわらかく撫でてくれるなんて、やさしいね。\e"

	"\1[[1005]]そうやって、あなたのことをいっぱい教えてね。\e"
}

MouseMove1Hand : nonoverlap
{
    SHIORI3FW.ResetAITalkInterval
	
	skinshipSuzuCount++
	
	Init
	--
	"\1[[1005]]ん。\w7\n[[1008]]握りたいなら握ってもいいんだよ？\e"

	"\1[[1016]]すべすべなお手手だよ。\n[[1000]]綺麗でしょう？\e"

	"\u[[1016]]\h[[60]]爪の色、綺麗ですよね。\n[[50]]目の色と合わせてるのかな。\n/
	\1[[1005]]そう、ウルトラマリンブルー。\n[[1000]]鈴ちゃんの色だよ。\e"

	"\1[[1000]]指輪、気になる？\n[[1016]]特に意味はない、ふつうの真鍮のリングだよ。\e"
}

MouseMove1Ear : nonoverlap
{
    SHIORI3FW.ResetAITalkInterval
	
	harassmentSuzuCount++
	fav_Suzu -= 2
	
	Init
	--
	"\1[[1001]]にゃ。\n………[[1003]]お耳をかわいがってくれるのはうれしいけど、ちょっとびっくりしちゃうな。\e"

	'\1[[1076]]………[[1007]]やだ。\e'

	'\1[[1009]]ん、こそばゆい……\e'

	"\1[[1003]]お耳触るの、他の猫ちゃんにはやらないでね？？\n[[1066]]嫌われちゃうよ。\e"

	"\1[[1039]]………。/
	\0[[18]]%(username)。\w6[[14]]嫌そうですよ、やめてあげてくださいな。\e"
}

MouseMove1Tail : nonoverlap
{
    SHIORI3FW.ResetAITalkInterval
	
	Init
	--
	"\1[[1032]]にゃっ！\n\w8[[1056]]……触るときは言ってね、びっくりしちゃうから。\e"

	"\1[[1005]]細くて長い尻尾だよ。\n[[1028]]かわいいでしょう？\e"

	"\1[[1016]]ん。やさしくなら、触ってもいいから………\e"

	"\1[[1000]]ごく普通の、ゆらゆら揺れてかわいらしいしっぽだよ。[[1015]]うらやましい？\e"

	"\1[[1005]]悠梨のしっぽよりかわいいと認めてくれたということでいいかしら？/
	\0[[87]]聞き捨てなりませんね。/
	\1[[1008]]ですってよ。\n[[1016]]ほら、悠梨のしっぽも触ってあげて。/
	\0[[22]]あの、それは、[[9]]遠慮してもらえると……\e"
}


//******************************************************************************
//つつかれ反応
//******************************************************************************

//本体側のつつかれ反応
MouseDoubleClick0Head : nonoverlap
{
    SHIORI3FW.ResetAITalkInterval
	
	hitYuriHeadCount++
	fav_Yuri -= 2

	Init + '\0'
	--
	'[[12]]い゛っ！'
	"[[32]]に゛っっ！！！\u[[1006]]良い声が出たわね。\0"
	'[[35]]ぎにゃっ！？'
	'[[2]]にゃっ！？'
	'[[12]]痛いっ！？'
	'[[2]]っ！！'
	--
	NewLine(3)
	--
	'\w9'
	--

	"[[6]]……コミュ:頭叩きは実装されていません。\n[[3]]されていませんので、どうか叩かないでクダサイ……/
	\1[[1098]]無理があるわよ、さすがに。\e"

	"[[14]]あの、なんなんですか、突然……\e"

	"………[[23]]私、なにか悪いことしました？？\e"

	"………[[17]]怒りますよ、[[37]]怒っていいんですね！？/
	\1[[1036]]怒らない人の言い方ね。\e"

	"[[42]]こういうのは鈴が喜ぶやつじゃないですか！\n[[83]]ね、ほら、鈴が見ていますよ。/
	\1[[1098]]見てない。\e"

	"[[85]]叩いてもいいですけど、私もちゃんと叩かれた回数を覚えてますからね！！/
	\1[[1068]]脅迫になるわよ。[[1006]]やめなさい。\e"
}

MouseDoubleClick0Hand : nonoverlap
{
    SHIORI3FW.ResetAITalkInterval
	
	skinshipYuriCount++
	fav_Yuri++

	Init
	--
	"\0[[5]]はい、握手。\n[[0]]ありがとうね、%(username)。\e"

	"\0[[76]]当店はお触り禁止となっております。/
	\1[[1055]]残念ね。\w4[[1000]]じゃあわたしと手を繋ぎましょ。/
	\0………[[23]]あの、ウソです。\n[[42]]だから仲間外れにしないで……\e"

	"\0[[16]]ん。\n[[95]]手ってあったかくて、すごいね。\e"

	"\0[[6]]こうやって手を重ねてみると、私の手と%(username)の手はぜんぜん違うね。\n[[5]]色も、形も、ぜんぶ。\e"

	"\0[[36]]手と手でやるコミュニケーション。\n[[5]]すごい発明だよね、%(username)？\e"
}


MouseDoubleClick0Ear : nonoverlap
{
	MouseMove0Ear
}

MouseDoubleClick0Tail : nonoverlap
{
	MouseMove0Tail
}

MouseDoubleClick1Head : nonoverlap
{
    SHIORI3FW.ResetAITalkInterval
	
	hitSuzuHeadCount++

	Init + '\1'
	--
	"[[1002]]っ！！"
	"[[1035]]んっ！"
	"[[1089]]うっ…！！"
	"[[1085]]にゃ！"
	"[[1002]]にゃっ！？"
	"[[1089]]んっ……"
	--
	NewLine(3)
	--
	'\w9'
	--

	'………[[1049]]はあ、もっとしてもいいんだよ？？\e'

	'[[1004]]………痛がった方がいいかな、[[1009]]それとも気持ちよくなった方がいい？？\e'

	"[[1043]]ふふ。わたしはいいけど、悠梨にはこういうことしないほうがいいよ。[[1005]]怒っちゃうからね。/
	\0[[66]]されるがままに叩かれてるほうがおかしいですよ……\e"

	"……[[1029]]気持ちよくても、それとは別にちゃんと痛いね。/
	\0[[64]]当たり前です。\e"

	"[[1018]]%(username)。\w8[[1058]]悠梨が怖がっているわよ。/
	\0[[13]]そんな、こと、[[66]]ないデス……\e"

	"[[1095]]えへへ。%(username)、しあわせだよ。\e"
}

MouseDoubleClick1Hand : nonoverlap
{
    SHIORI3FW.ResetAITalkInterval
	
	skinshipSuzuCount++

	Init
	--
	'\1[[1016]]はい。[[1000]]あなたの鈴はここにいますよ。\e'

	"\1[[1016]]ん、あくしゅ。\n[[1005]]コミュニケーションの基本だね。\e"

	"\1[[1050]]ほら、悠梨もおいで。[[1005]]三人で手を繋ごう？/
	\0[[3]]いや、私は、[[39]]恥ずかしいから……\e"

	"\1[[1006]]きゅ。\n[[1055]]手を握る、ちいさいけど確かな音。\e"
}

MouseDoubleClick1Ear
{
	MouseMove1Ear
}

MouseDoubleClick1Tail
{
	MouseMove1Tail
}

MouseDoubleClick0
{
	//顔以外のつつきでメニュー展開
	OpenYuriMenu
}

//kero側のつつかれ反応
MouseDoubleClick1
{
	OpenSuzuMenu
}

//******************************************************************************
//マウス処理部コア
//AYATEMPLATE.MouseMoveWheelFuncの2番目の数字が「感度」です。
//1番目の数字（0か1）は、MoveかWheelかを判別するためのもので、
//変更の必要はありません。
//******************************************************************************

OnMouseMove
{
	AYATEMPLATE.MouseMoveWheelFunc(0,40,'MouseMove')
}

OnMouseWheel
{
	_result = ''
	if TOINT(reference[2]) > 0 {
		_result = AYATEMPLATE.MouseMoveWheelFunc(1,4,'MouseWheelUp')
	}
	else {
		_result = AYATEMPLATE.MouseMoveWheelFunc(1,4,'MouseWheelDown')
	}

	if _result == '' {
		_result = AYATEMPLATE.MouseMoveWheelFunc(1,4,'MouseWheel')
	}

	_result
}

OnMouseDoubleClick
{
	AYATEMPLATE.MouseEventExec('MouseDoubleClick')
}
