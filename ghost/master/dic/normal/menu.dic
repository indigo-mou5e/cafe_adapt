//******************************************************************************
// メニュー処理辞書
//******************************************************************************

//******************************************************************************
//メインメニューの展開
//
//AYATEMPLATE.MenuItemの使い方
//1つめの文字列＝選択肢に表示する文字列
//2つめの文字列＝選んだときに実行される関数名
//3つめの文字列＝改行する量　50%刻み　省略すると100%（＝\n）
//******************************************************************************

//------------------------------------------------------------------------------
//メインメニュー展開
//------------------------------------------------------------------------------
OpenYuriMenu
{
	Init
	--
	if dicMode != MODE_SUZU {
		'\0\s[5]はい、メニューです。\n\n'
		"\0\s[5]%(username)、なんですか？\n\n"
		'\0\s[0]ん、どうしたの？\n\n'
	}
	--
	YuriMenuList
}

YuriMenuList
{
	if dicMode != MODE_SUZU
		AYATEMPLATE.MenuItem('ランダムトーク', 'Menu_AITALK', 0)
	--
	if (STRLEN(AYATEMPLATE.LastTalk) > 0 && dicMode != MODE_SUZU) {
		AYATEMPLATE.MenuItem('さっきの話は？', 'Menu_LASTTALK')
	}
	--
	/*
	if dicMode != MODE_SUZU
		AYATEMPLATE.MenuItem('悠梨に話しかける', 'Menu_COMMUNICATE')
		*/
	--
	AYATEMPLATE.MenuItem('自己紹介を聞く', 'IntroductionYuri',150)
	--
	AYATEMPLATE.MenuItem('質問コーナー','FAQ_Top')
	--
	// AYATEMPLATE.MenuItem('ミニゲーム【リバーポーカー】', 'PokerMenu')
	--
	AYATEMPLATE.MenuItem('トーク設定', 'Menu_TALKSETTING',150)
	--
	AYATEMPLATE.MenuItem('バージョン情報', 'Menu_Version')
	--
	/*
	if dicMode != MODE_SUZU
		AYATEMPLATE.MenuItem('他のゴーストと話す', 'Menu_GHOSTCOMSTART')
	*/
	--
	// AYATEMPLATE.MenuItem("初起動トークを再生","Menu_FIRSTBOOT")
	--
	/*
	//スタンプ帳チェック
	if AYATEMPLATE.IsPluginExist('379E0DA0-131C-11DD-E1AC-90009E101CFE') {
		AYATEMPLATE.MenuItem('スタンプを押す', 'Menu_STAMP')
	}
	*/
	--
	/*
	case dicMode {
		when MODE_NORMAL
			AYATEMPLATE.MenuItem('悠梨と二人で話す', 'Menu_ChangeYuriMode', 150)
		when MODE_YURI {
			AYATEMPLATE.MenuItem('三人モードに戻る', 'Menu_ChangeNormalMode', 150) + /
				AYATEMPLATE.MenuItem('鈴と二人で話す', 'Menu_ChangeSuzuMode')
		}
	}
	*/
	--
	AYATEMPLATE.MenuItem('なんでもない', 'YuriMenu_CANCEL', 150)
}

OpenSuzuMenu
{
	Init
	--
	if dicMode != MODE_YURI {
		'\1\s[1055]ん、なあに？\n\n'
		"\1\s[1000]%(username)、なにか御用？\n\n"
		'\1\s[1000]はい、なんでしょう？\n\n'
	}
	--
	SuzuMenuList
}

SuzuMenuList
{
	'\1\b[2]'
	--
	if dicMode != MODE_YURI
		AYATEMPLATE.MenuItem('ランダムトーク', 'Menu_AITALK', 0)
	--
	if (STRLEN(AYATEMPLATE.LastTalk) > 0) {
		AYATEMPLATE.MenuItem('さっきの話は？', 'Menu_LASTTALK')
	}
	--
	AYATEMPLATE.MenuItem('鈴に話しかける', 'Menu_COMMUNICATE')
	--
	AYATEMPLATE.MenuItem('自己紹介を聞く', 'IntroductionSuzu')
	--
	AYATEMPLATE.MenuItem('ユーザ情報', 'Menu_UserInformation')
	--
	// AYATEMPLATE.MenuItem('【デバッグ】トランプテスト', 'Debug_RandomCardTest')
	--
	/*
	case dicMode {
		when MODE_NORMAL
			AYATEMPLATE.MenuItem('鈴と二人で話す', 'Menu_ChangeSuzuMode', 150)
		when MODE_SUZU {
			AYATEMPLATE.MenuItem('三人モードに戻る', 'Menu_ChangeNormalMode', 150) + /
				AYATEMPLATE.MenuItem('悠梨と二人で話す', 'Menu_ChangeYuriMode')
		}
	}
	*/
	--
	AYATEMPLATE.MenuItem('なんでもない', 'SuzuMenu_CANCEL', 150)
}

//------------------------------------------------------------------------------
//メニュー選択キャンセル
//------------------------------------------------------------------------------
YuriMenu_CANCEL
{
	Init
	--
	if dicMode != MODE_SUZU {
		'\0\s[6]そう。\e'
		'\0\s[16]ん、わかった。\e'
	}
}

SuzuMenu_CANCEL
{
	Init
	--
	if dicMode != MODE_YURI {
		'\1\s[1016]そう。\e'
		'\1\s[1006]ん。\e'
	}
}

Menu_Close
{
	'\h\s[16]\u\s[1016]\e'
}

//******************************************************************************
//各項目の処理内容
//******************************************************************************

Menu_FIRSTBOOT
{
	OnFirstBoot
}

//---- 何か話して
Menu_AITALK
{
	//---- 明示的にAIトーク開始を指示
	OnAiTalk
}

//---- さっきの話は
Menu_LASTTALK
{
	AYATEMPLATE.LastTalk
}

//---- トーク頻度の調整
Menu_TALKSETTING
{
	'\_q\0\s[0]\f[align,center]---- ランダムトーク間隔 ----\n'
	--
	"\f[align,center]\q[%(Choice_TaikInterval(60)),SetTalkInterval_60] /
	\q[%(Choice_TaikInterval(120)),SetTalkInterval_120] /
	\q[%(Choice_TaikInterval(180)),SetTalkInterval_180] /
	\q[%(Choice_TaikInterval(300)),SetTalkInterval_300] /
	\q[%(Choice_TaikInterval(600)),SetTalkInterval_600] /
	\q[%(Choice_TaikInterval(0)),SetTalkInterval_None]"
	--
	'\n\n\f[align,center]---- 自動ウェイトの待ち時間 ----\n/
	\f[align,center]（大きいほど句読点や改行時の待ち時間が　\n/
	\f[align,center]　増えてトークがゆっくりになります）\n'
	--
	"\f[align,center]/
	\q[%(Choice_TalkAutoWait(1)),SetTalkWait_1]　/
	\q[%(Choice_TalkAutoWait(2)),SetTalkWait_2]　/
	\q[%(Choice_TalkAutoWait(3)),SetTalkWait_3]　/
	\q[%(Choice_TalkAutoWait(4)),SetTalkWait_4]　/
	\q[%(Choice_TalkAutoWait(5)),SetTalkWait_5]　/
	\q[%(Choice_TalkAutoWait(6)),SetTalkWait_6]　/
	\q[%(Choice_TalkAutoWait(7)),SetTalkWait_7]"
	--
	"\n\n\f[align,center] ---- 撫で反応の敏感さ ----\n\f[align,center]/
	\q[%(Choice_NadeSensitivity(0)),SetNadeSense_0]　/
	\q[%(Choice_NadeSensitivity(1)),SetNadeSense_1]　/
	\q[%(Choice_NadeSensitivity(2)),SetNadeSense_2]\n\n[half]\f[align,left]"
	--
	AYATEMPLATE.MenuItem('戻る', 'Menu_Close',0)
	--
	'\_q'
}

Choice_TaikInterval
{
	if _argv[0] <= 0 {
		if aitalkinterval <= 0
			'●喋らない'
		else
			'○喋らない'
	}
	else {
		if _argv[0] == aitalkinterval
			"●%(_argv[0])s"
		else
			"○%(_argv[0])s"
	}
}

Choice_TalkAutoWait
{
	if _argv[0] == talkAutoWait {
		'●'
	}
	else
	{
		'○'
	}
	--
	case _argv[0]
	{
		when 1{
			'１'
		}
		when 2{
			'２'
		}
		when 3{
			'３'
		}
		when 4{
			'４'
		}
		when 5{
			'５'
		}
		when 6{
			'６'
		}
		when 7{
			'７'
		}
	}
}

Choice_NadeSensitivity
{
	if _argv[0] == nadeSensitivity {
		'●'
	}
	else
	{
		'○'
	}
	--
	case _argv[0]
	{
		when 0{
			'敏感'
		}
		when 1{
			'ふつう'
		}
		when 2{
			'にぶい'
		}
	}
}

SetNadeSense_0
{
	nadeSensitivity = 0

	Menu_TALKSETTING
}

SetNadeSense_1
{
	nadeSensitivity = 1

	Menu_TALKSETTING
}

SetNadeSense_2
{
	nadeSensitivity = 2

	Menu_TALKSETTING
}

SetTalkWait_1
{
	talkAutoWait = 1

	Menu_TALKSETTING
}

SetTalkWait_2
{
	talkAutoWait = 2

	Menu_TALKSETTING
}

SetTalkWait_3
{
	talkAutoWait = 3

	Menu_TALKSETTING
}

SetTalkWait_4
{
	talkAutoWait = 4

	Menu_TALKSETTING
}

SetTalkWait_5
{
	talkAutoWait = 5

	Menu_TALKSETTING
}

SetTalkWait_6
{
	talkAutoWait = 6

	Menu_TALKSETTING
}

SetTalkWait_7
{
	talkAutoWait = 7

	Menu_TALKSETTING
}

SetTalkInterval_60
{
	aitalkinterval = 60

	Menu_TALKSETTING
	
	/*
	Init
	--
	'\0[[16]]60秒に設定しました。\w9\1[[1016]]それは篠突く雨のように。\e'
	*/
}

SetTalkInterval_120
{
	aitalkinterval = 120

	Menu_TALKSETTING
	
	/*
	Init
	--
	'\0[[16]]120秒に設定しました。\1[[1018]]話すことがなくなっても知らないよ？\e'
	*/
}

SetTalkInterval_180
{
	aitalkinterval = 180

	Menu_TALKSETTING
	
	/*
	Init
	--
	'\0[[16]]180秒に設定しました。\1[[1055]]いつもどおり、だね。\e'
	*/
}

SetTalkInterval_300
{
	aitalkinterval = 300

	Menu_TALKSETTING
	
	/*
	Init
	--
	'\0[[16]]300秒に設定しました。\1[[1016]]まったりゆっくり、ね。\e'
	*/
}

SetTalkInterval_600
{
	aitalkinterval = 600

	Menu_TALKSETTING
	
	/*
	Init
	--
	'\0[[16]]600秒に設定しました。\1[[1006]]……ちょっと退屈かも。\e'
	*/
}

SetTalkInterval_None
{
	aitalkinterval = 0

	Menu_TALKSETTING
	
	/*
	Init
	--
	'\0[[0]]じゃあこちらからは話しかけないでおくね。\1[[1055]]またお話聞きたくなったら言ってね。\e'
	*/
}

Menu_Version
{
	Init
	--
	"\h\_qAdaptation / %(GetVersion)\n/
	Shiori：YAYA(Version %(SHIORI3EV.On_version))\n\n[half]/
	総ランダムトーク数：%(OUTPUTNUM('RandomTalkBasic'))\n/
	前回の更新内容：「%(LastUpdate)」\_q"
	--
	AYATEMPLATE.MenuItem('更新履歴を見る', 'OpenVersionHistory',200)
	--
	AYATEMPLATE.MenuItem('ネットワーク更新', 'Menu_UpdateMyself')	
	--
	AYATEMPLATE.MenuItem('戻る', 'YuriMenuList', 200)
	--
	AYATEMPLATE.MenuItem('閉じる', 'Menu_Close')
}

Menu_UpdateMyself
{
	'\![updatebymyself]'
}

OpenVersionHistory
{
	'\![open,file,..\..\version.txt]\e'
}

Menu_UserInformation
{
	Init
	--
	"\_q\0\![*]総起動回数　　：%(bootCount)回\n/
	\![*]合計起動時間　：%(GetTotalOperation_hour)時間%(GetTotalOperation_minute)分%(GetTotalOperation_second)秒\n/
	\![*]今回の起動時間：%(GetCurrentOperation_hour)時間%(GetCurrentOperation_minute)分%(GetCurrentOperation_second)秒\n\n[half]/
	\_l[4em,]\![*]鈴\_l[16em,]\![*]悠梨\n/
	\_l[3em,]好感度：%(fav_Suzu)\_l[16em,]好感度：%(fav_Yuri)\n/

	\1\_q"
	--
	AYATEMPLATE.MenuItem("ユーザ名称　：%(username)", 'InputUserName', 0)
	--
	AYATEMPLATE.MenuItem("ユーザ性別　：%(GetUserGender)", 'SelectUserGender', 100)
	--
	"\_q\n\![*]ユーザ誕生日：%(userBirthMonth)月%(userBirthDay)日\_q"
	--
	AYATEMPLATE.MenuItem('戻る', 'SuzuMenuList', 150)
	--
	AYATEMPLATE.MenuItem('閉じる', 'Menu_Close')
}

InputUserName
{
	'\0なんてお呼びしましょうか？/
	\![open,inputbox,SetUserName,-1]'
}

SetUserName
{
	if (_argv[0] == INPUT_CLOSE) || (_argv[0] == INPUT_TIMEOUT) || (_argv[0] == '') || (_argv[0] == username) {
		CancelInputUserName
		return
	}

	username = _argv[0]

	Init
	--
	"\0それでは、これからは%(username)とお呼びしますね。\e"
}

CancelInputUserName
{
	Init
	--
	"\0では、これまで通り%(username)とお呼びしますね。\e"
}

GetUserGender
{
	case userGender {
		when 0 {
			'ひみつ'
			'内緒'
			'不定'
			'不明'
		}
		when 1 {
			'男性'
			'男の子'
		}
		when 2 {
			'女性'
			'女の子'
		}
	}
}

SelectUserGender
{
	'\0どんな風に振る舞いたいのかな？\n\n/
	\_q\q[男性,SetGenderMale]\n/
	\q[女性,SetGenderFemale]\n/
	\q[不定,SetGenderXXX]\_q'
}

SetGenderMale
{
	userGender = 1

	'\0[[16]]では、そのように。'
}

SetGenderFemale
{
	userGender = 2

	'\0[[16]]では、そのように。'
}

SetGenderXXX
{
	userGender = 0

	'\0[[16]]では、そのように。'
}

Menu_ChangeNormalMode
{
	dicMode = MODE_NORMAL
	AYATEMPLATE.LastTalk = ''

	Init
	--
	"\0\s[0]\1\s[1000]じゃあまた三人でお話しましょうか。/
	\0よろしく、%(username)。\e"
}

Menu_ChangeYuriMode
{
	dicMode = MODE_YURI
	AYATEMPLATE.LastTalk = ''

	Init
	--
	"\1\s[1000]それじゃあわたしはキッチンの方に行ってくるね？\nいつでも呼んでいいからね、%(username)。\s[1999]/
	\0\s[0]……さて、何を話せばいいのかな？\e"
}

Menu_ChangeSuzuMode
{
	dicMode = MODE_SUZU
	AYATEMPLATE.LastTalk = ''

	Init
	--
	"\0\s[0]オッケー。私は猫たちの世話でもしてこようかな。また後でね、%(username)。\s[999]/
	\1\s[1000]……ふたりきり、だね。ね、%(username)？"
}

//---- ややめに話しかける
Menu_COMMUNICATE
{
	'\0んー？'
	'\0なに？'
	'\0？'
	--
	'\![open,communicatebox]\e'
}

//---- 他のゴーストと話す
Menu_GHOSTCOMSTART
{
	StartCommunicate
}

//---- ヘッドライン
Menu_HEADLINECANCEL
{
	Init
	--
	//---- キャンセルされた
	'\0\s[5]はーい。\e'
}

//---- スタンプ帳
Menu_STAMP
{
	//初期化
	SHIORI3FW.RegisterTempVar('StampInfo_IsExist', 'StampInfo_IsNotify', 'StampInfo_CurrentStampFile', 'StampInfo_CurrentStampTitle')

	StampInfo_IsExist = 0
	StampInfo_IsNotify = 0

	//スタンプ帳情報を取った後、raiseでOnStampMenuExecを発生させて下に処理を移す
	'\![raiseplugin,379E0DA0-131C-11DD-E1AC-90009E101CFE,OnStampInfoCall]\![raise,OnStampMenuExec]'
}

OnStampInfo
{
	//スタンプあるかどうか？
	StampInfo_IsExist = reference[1]
	StampInfo_IsNotify = 1
	StampInfo_CurrentStampFile = ''
	StampInfo_CurrentStampTitle = ''

	//押している場合は、どのスタンプを押しているか？
	_stamparray = SPLIT(reference[3], CHR(2))

	StampInfo_Data = _stamparray

	_n = ARRAYSIZE(_stamparray)
	for _i=0; _i<_n; _i++ {
		_stamp = RE_SPLIT(_stamparray[_i], '[\x1,]') //バイト値1かカンマかでぶった切る：互換モード対策

		//ほかで使う場合はここの名前を変えてね
		if _stamp[0] == 'はろーYAYAわーるど' || _stamp[0] == '紺野ややめ' {
			StampInfo_CurrentStampFile = _stamp[1]
			StampInfo_CurrentStampTitle = _stamp[2]
			break
		}
	}
}

OnStampMenuExec
{
	_stamp_type = ''

	//あった
	if StampInfo_IsExist {
		if 'ややめ' _in_ StampInfo_CurrentStampTitle {
			//マック朗におきかえ
			'\u\s[1000]\h\s[2]もう押してあるのに、\w5まだほしいの？\w9\w9\u\s[1002]ならおれのスタンプに置き換えだ！\w9\nどうだうれしいだろ！\w9\w9\h\s[4]\n\nなにやってるの\w5…\w5…'

			_stamp_type = 'マック朗'
		}
		else {
			//ややめに置き換え
			'\h\s[0]\u\s[1000]もうおれのスタンプが押してあるぞ？\w9\w9\h\s[5]とりあえずわたしのに戻すねー。'

			_stamp_type = 'ややめ'
		}
	}
	//ない
	else {
		'\0\s[0]スタンプ？\w9\w9\n\s[5]はいどうぞ～。\w9\w9\1\s[1002]せっかくだから俺はこのスタンプを選ぶぜ！\w9\w9\0\s[4]えーっと。'

		_stamp_type = 'ややめ'
	}
	--
	if _stamp_type == 'ややめ' {
		//2行目ファイル、3行目タイトル、4行目コメント
		'\![raiseplugin,379E0DA0-131C-11DD-E1AC-90009E101CFE,OnStampAdd,/
		ghost/master/stamp/stmp_yym.png,/
		ややめスタンプ,/
		紺野ややめが押したスタンプ。服のジッパーの取っ手とデザインが同じ。]'
	}
	else {
		'\![raiseplugin,379E0DA0-131C-11DD-E1AC-90009E101CFE,OnStampAdd,/
		ghost/master/stamp/stmp_mcr.png,/
		マック朗スタンプ,/
		マック朗が押したスタンプ。黒林檎。見た目に反して毒はない…と信じたい。]'
	}
}