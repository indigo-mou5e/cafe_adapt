//******************************************************************************
// メニュー処理辞書
//******************************************************************************

// サーフェス短縮表記
#define [[ \s[
#define ]] ]

//******************************************************************************
//メインメニューの展開
//
//AYATEMPLATE.MenuItemの使い方
//1つめの文字列＝選択肢に表示する文字列
//2つめの文字列＝選んだときに実行される関数名
//3つめの文字列＝改行する量　50%刻み　省略すると100%（＝\n）
//******************************************************************************

Menu_Close
{
	case dicMode{
		when MODE_NORMAL{
			'\h\s[16]\u\s[1016]\e'
		}
		when MODE_YURI{
			'\h\s[16]\u\s[1999]\e'
		}
		when MODE_SUZU{
			'\h\s[999]\u\s[1016]\e'
		}
	}
}

//******************************************************************************
//各項目の処理内容
//******************************************************************************


//---- 何か話して
Menu_AITALK
{
	//---- 明示的にAIトーク開始を指示
	OnAiTalk
}

//---- さっきの話は
Menu_LASTTALK
{
	AYATEMPLATE.LastTalk
}

Menu_ChangeNormalMode
{
	dicMode = MODE_NORMAL
	AYATEMPLATE.LastTalk = ''
	SHIORI3FW.EndChainTalk

	Init
	--
	"\0\s[0]\1\s[1000]じゃあまた三人でお話しましょうか。/
	\0[[25]]よろしく、%(username)。\e"
}

Menu_ChangeYuriMode
{
	dicMode = MODE_YURI
	AYATEMPLATE.LastTalk = ''
	SHIORI3FW.EndChainTalk

	Init
	--
	"\1\s[1016]それじゃあわたしはキッチンの方に行ってくるね？\n[[1015]]いつでも呼んでいいからね、%(username)。\s[1999]/
	\0\s[5]……さて、何を話せばいいのかな？\e"
}

Menu_ChangeSuzuMode
{
	dicMode = MODE_SUZU
	AYATEMPLATE.LastTalk = ''
	SHIORI3FW.EndChainTalk

	Init
	--
	"\0\s[6]オッケー。私は猫たちの世話でもしてこようかな。\n[[5]]また後でね、%(username)。\s[999]/
	\1\s[1055]……ふたりきり、だね。\n[[1000]]ね、%(username)？"
}

/*
//---- ややめに話しかける
Menu_COMMUNICATE
{
	'\0んー？'
	'\0なに？'
	'\0？'
	--
	'\![open,communicatebox]\e'
}

//---- 他のゴーストと話す
Menu_GHOSTCOMSTART
{
	StartCommunicate
}
*/

//---- ヘッドライン
Menu_HEADLINECANCEL
{
	Init
	--
	if dicMode != MODE_SUZU{
		'\0\s[16]ん、終わりね。\e'
	}
	else{
		'\1\s[1016]ヘッドライン、おしまい。\e'
	}
}

//---- スタンプ帳
Menu_STAMP
{
	//初期化
	SHIORI3FW.RegisterTempVar('StampInfo_IsExist', 'StampInfo_IsNotify', 'StampInfo_CurrentStampFile', 'StampInfo_CurrentStampTitle')

	StampInfo_IsExist = 0
	StampInfo_IsNotify = 0

	//スタンプ帳情報を取った後、raiseでOnStampMenuExecを発生させて下に処理を移す
	'\![raiseplugin,379E0DA0-131C-11DD-E1AC-90009E101CFE,OnStampInfoCall]\![raise,OnStampMenuExec]'
}

OnStampInfo
{
	//スタンプあるかどうか？
	StampInfo_IsExist = reference[1]
	StampInfo_IsNotify = 1
	StampInfo_CurrentStampFile = ''
	StampInfo_CurrentStampTitle = ''

	//押している場合は、どのスタンプを押しているか？
	_stamparray = SPLIT(reference[3], CHR(2))

	StampInfo_Data = _stamparray

	_n = ARRAYSIZE(_stamparray)
	for _i=0; _i<_n; _i++ {
		_stamp = RE_SPLIT(_stamparray[_i], '[\x1,]') //バイト値1かカンマかでぶった切る：互換モード対策

		//ほかで使う場合はここの名前を変えてね
		if _stamp[0] == 'はろーYAYAわーるど' || _stamp[0] == '紺野ややめ' {
			StampInfo_CurrentStampFile = _stamp[1]
			StampInfo_CurrentStampTitle = _stamp[2]
			break
		}
	}
}

OnStampMenuExec
{
	_stamp_type = ''

	//あった
	if StampInfo_IsExist {
		if 'ややめ' _in_ StampInfo_CurrentStampTitle {
			//マック朗におきかえ
			'\u\s[1000]\h\s[2]もう押してあるのに、\w5まだほしいの？\w9\w9\u\s[1002]ならおれのスタンプに置き換えだ！\w9\nどうだうれしいだろ！\w9\w9\h\s[4]\n\nなにやってるの\w5…\w5…'

			_stamp_type = 'マック朗'
		}
		else {
			//ややめに置き換え
			'\h\s[0]\u\s[1000]もうおれのスタンプが押してあるぞ？\w9\w9\h\s[5]とりあえずわたしのに戻すねー。'

			_stamp_type = 'ややめ'
		}
	}
	//ない
	else {
		'\0\s[0]スタンプ？\w9\w9\n\s[5]はいどうぞ～。\w9\w9\1\s[1002]せっかくだから俺はこのスタンプを選ぶぜ！\w9\w9\0\s[4]えーっと。'

		_stamp_type = 'ややめ'
	}
	--
	if _stamp_type == 'ややめ' {
		//2行目ファイル、3行目タイトル、4行目コメント
		'\![raiseplugin,379E0DA0-131C-11DD-E1AC-90009E101CFE,OnStampAdd,/
		ghost/master/stamp/stmp_yym.png,/
		ややめスタンプ,/
		紺野ややめが押したスタンプ。服のジッパーの取っ手とデザインが同じ。]'
	}
	else {
		'\![raiseplugin,379E0DA0-131C-11DD-E1AC-90009E101CFE,OnStampAdd,/
		ghost/master/stamp/stmp_mcr.png,/
		マック朗スタンプ,/
		マック朗が押したスタンプ。黒林檎。見た目に反して毒はない…と信じたい。]'
	}
}

//******************************************************************************
// イベントメニュー
//******************************************************************************

SpecialEventMenu
{
	_events = ''

	_day = month * 100 + day

	case _day {
		when 1225 {
			if year != lastYearOnMerryChristmasYuri
				_events += AYATEMPLATE.MenuItem('悠梨、メリークリスマス！', 'MerryChristmas_Yuri')
			
			if year != lastYearOnMerryChristmasSuzu
				_events += AYATEMPLATE.MenuItem('鈴、メリークリスマス！', 'MerryChristmas_Suzu')
		}

		when 1226-1230{		
			_events += AYATEMPLATE.MenuItem('年末年始の営業日を聞く', 'AskNewYearSchedule')
		}

		when 101, 1231 {
			_events += AYATEMPLATE.MenuItem('なにか訊いてみる', 'NewYearAskingTop')
		}
	}

	if _events == '' {
		return
	}

	"\2\_q―― 特別メニュー ――%(_events)\_q\0"
}

//******************************************************************************
// デバッグモード
//******************************************************************************

Debug_TopMenu
{
	Init
	--
	'\0[[0]]はい。デバッグモード、実行中です。/
	\1[[1006]]もし万が一迷い込んだ場合は、「デバッグモード終了」を選べば元に戻るわよ。\0'
	--
	AYATEMPLATE.MenuItem('各種イベント', 'Debug_EventTest', 150)
	--
	AYATEMPLATE.MenuItem('スクリーンショット用テキスト', 'Debug_ForPrintScreen')
	--
	AYATEMPLATE.MenuItem('デバッグモード終了', 'Debug_SwitchDebugModeOff')
	--
	AYATEMPLATE.MenuItem('閉じる', 'Menu_Close', 150)
}

Debug_SwitchIsFirstBoot
{
	if isFirstBootOnTheDay == 0 {
		isFirstBootOnTheDay = 1
		'\0[[16]]ん。初回起動かどうかを切り替えましたよ。\n/
		[[0]]今は「その日初回の起動」です。\e'
	}
	else{
		isFirstBootOnTheDay = 0
		'\0[[16]]ん。初回起動かどうかを切り替えましたよ。\n/
		[[0]]今は「その日2回目以降の起動」です。\e'
	}

}

Debug_EventTest
{
	Init
	--
	'\0[[16]]各種イベントにジャンプできます。'
	--
	AYATEMPLATE.MenuItem('初回起動', 'FirstBootTalk', 150)
	--
	AYATEMPLATE.MenuItem('誕生日イベント', 'Debug_UserBirthDayTest')
	--
	AYATEMPLATE.MenuItem('クリスマスイベント', 'Debug_ChristmasTest')
	--
	AYATEMPLATE.MenuItem('年末年始イベント', 'Debug_NewYearTest')
	--
	AYATEMPLATE.MenuItem('閉じる', 'Menu_Close', 150)
}

Debug_UserBirthDayTest
{
	Init
	--
	'\0[[0]]誕生日イベントです。\nどの条件ではじめましょうか？'
	--
	if isFirstBootOnTheDay == 0 {
		"\_q%(NewLine(2,0))　その日初回起動かどうか：false\_q"
	}
	else{
		"\_q%(NewLine(2,0))　その日初回起動かどうか：true\_q"
	}
	--
	AYATEMPLATE.MenuItem('誕生日初回', 'Debug_BirthDayEvent', 'First')
	--
	AYATEMPLATE.MenuItem('去年以前に祝われ済み', 'Debug_BirthDayEvent', '2000')
	--
	AYATEMPLATE.MenuItem('2回目以降の起動', 'Debug_BirthDayEvent', 'ThisYear')
	--
	AYATEMPLATE.MenuItem('メニューからスペシャル猫パフェを選択', 'ChooseBirthDayParfait')
	--
	AYATEMPLATE.MenuItem('初回起動かどうかを切り替え', 'Debug_SwitchIsFirstBoot', 150)
	--
	AYATEMPLATE.MenuItem('閉じる', 'Menu_Close', 150)
}

Debug_ChristmasTest
{
	Init
	--
	'\0[[0]]クリスマスイベントです。\nどの条件ではじめましょうか？'
	--
	if isFirstBootOnTheDay == 0 {
		"\_q%(NewLine(2,0))　その日初回起動かどうか：false\_q"
	}
	else{
		"\_q%(NewLine(2,0))　その日初回起動かどうか：true\_q"
	}
	--
	AYATEMPLATE.MenuItem('24日に起動', 'BootOnChristmasEve')
	--
	AYATEMPLATE.MenuItem('25日に起動', 'BootOnChristmas')
	--
	AYATEMPLATE.MenuItem('「悠梨、メリークリスマス！」', 'MerryChristmas_Yuri')
	--
	AYATEMPLATE.MenuItem('「鈴、メリークリスマス！」', 'MerryChristmas_Suzu')
	--
	AYATEMPLATE.MenuItem('クリスマス祝い履歴をリセット', 'Debug_ResetLastChristmas')
	--
	AYATEMPLATE.MenuItem('初回起動かどうかを切り替え', 'Debug_SwitchIsFirstBoot', 150)
	--
	AYATEMPLATE.MenuItem('閉じる', 'Menu_Close', 150)
}

Debug_ResetLastChristmas
{
	lastYearOnMerryChristmasYuri = 0
	lastYearOnMerryChristmasSuzu = 0

	'\0[[0]]ん。またプレゼントをあげられるようになりましたよ。\e'
}

Debug_NewYearTest
{
	Init
	--
	'\0[[0]]年末年始イベントです。\nどの条件ではじめましょうか？'
	--
	if isFirstBootOnTheDay == 0 {
		"\_q%(NewLine(2,0))　その日初回起動かどうか：false"
	}
	else{
		"\_q%(NewLine(2,0))　その日初回起動かどうか：true"
	}
	--
	case progressOnNewYear{
		when 0 {
			"%(NewLine(2,0))　進捗フラグ：（初期状態）"
		}
		when 10 {
			"%(NewLine(2,0))　進捗フラグ：（営業日を聞いた）"
		}
		when 20 {
			"%(NewLine(2,0))　進捗フラグ：（大晦日に訪問済）"
		}
		when 30 {
			"%(NewLine(2,0))　進捗フラグ：（元日に訪問済）"
		}
		others {
			"%(NewLine(2,0))　進捗フラグ：（エラー。なんかヘンかも）"
		}
	}
	--
	"%(NewLine(2,0))　最後の新年：%(lastYearOnNewYear)\_q"
	--
	AYATEMPLATE.MenuItem('大晦日に起動', 'BootOnNewYearsDayEve', 150)
	--
	AYATEMPLATE.MenuItem('元日に起動', 'BootOnNewYearsDay')
	--
	AYATEMPLATE.MenuItem('悠梨に何か訊く', 'NewYearAskingTop')
	--
	AYATEMPLATE.MenuItem('フラグ変更', 'Debug_ChangeNewYearProgress')
	--
	AYATEMPLATE.MenuItem('初回起動かどうかを切り替え', 'Debug_SwitchIsFirstBoot', 150)
	--
	AYATEMPLATE.MenuItem('閉じる', 'Menu_Close', 150)
}

Debug_ChangeNewYearProgress
{
	Init
	--
	'\0[[0]]はーい。\nどうしましょうか？？'
	--
	AYATEMPLATE.MenuItem('初期状態にする', 'Debug_SetNewYearProgress', 'Init')
	--	
	AYATEMPLATE.MenuItem('営業時間を聞いた', 'Debug_SetNewYearProgress', 'Asked')
	--
	AYATEMPLATE.MenuItem('大晦日に訪れている', 'Debug_SetNewYearProgress', 'BootEve')
	--
	AYATEMPLATE.MenuItem('元日に訪れている', 'Debug_SetNewYearProgress', 'BootNewYear')
	--
	AYATEMPLATE.MenuItem('去年訪れた', 'Debug_SetNewYearProgress', 'LastYear', 150)
	--
	AYATEMPLATE.MenuItem('10年前訪れた', 'Debug_SetNewYearProgress', 'TenYearsAgo')
	--
	AYATEMPLATE.MenuItem('もどる', 'Debug_NewYearTest', 150)
}

Debug_SetNewYearProgress
{
	case temp_ChoiseExRef{
		when 'Init'{
			lastYearOnNewYear = 0
			progressOnNewYear = 0
		}
		when 'Asked'{
			progressOnNewYear = 10
		}
		when 'BootEve'{
			progressOnNewYear = 20
		}
		when 'BootNewYear'{
			progressOnNewYear = 30
		}
		when 'LastYear'{
			lastYearOnNewYear = year
			progressOnNewYear = 20
		}
		when 'TenYearsAgo'{
			lastYearOnNewYear = year - 10
			progressOnNewYear = 20
		}
	}

	Debug_NewYearTest
}

Debug_BirthDayEvent
{
	case temp_ChoiseExRef{
		when 'First'{
			ERASEVAR('lastYearOnCelebrateBirthDay')
		}
		when '2000'{
			lastYearOnCelebrateBirthDay = 2000
		}
		when 'ThisYear'{
			lastYearOnCelebrateBirthDay = year
		}
	}

	BootOnUserBirthDay
}

Debug_SwitchDebugModeOff
{
	isDebugMode = 0

	Init
	--
	'\0[[6]]デバッグモード終了。\n/
	[[0]]システム、通常モードに移行します。\e'
}

Debug_ForPrintScreen
{
	Init
	--
	"\0\s[0]ここは保護猫カフェ「アダプト」。\n行き先のない猫たちを保護したり、里親さんと巡り合わせたりするお店。/
	\1\s[1000]わたしたちはここのウェイターだよ。\nわたしが鈴、こっちの真面目なのが悠梨っていうの。/
	\n\n[half]気が向いた時に遊びに来て、わたしたちとお話ししてくれたらうれしいな。\e"
}