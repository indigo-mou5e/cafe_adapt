//******************************************************************************
// よくある質問のコーナー
//******************************************************************************

// 自動でトーク中の改行にウェイトを追加
#define \n[half] %(NewLine(1))
#define \n %(NewLine(2))

// 最初のスコープ変更以外に改行とウェイトを追加
#define '\0 '\h
#define '\1 '\u
#define "\0 "\h
#define "\1 "\u
#define \0 %(NewLine(3))\0
#define \1 %(NewLine(3))\1

// 鈴の一人称を書き換え
#define わたし %(GetSuzuMyself)

// サーフェス短縮表記
#define [[ \s[
#define ]] ]

// 1ページに何個質問を表示するか
#define QUESTIONS_ON_A_PAGE 2

//******************************************************************************
// FAQ
//******************************************************************************

FAQ_Top
{
    SHIORI3FW.ResetAITalkInterval

    Init
    --
    "\u[[1000]]\h[[0]]ここは%(username)が私たちに対して思ってそうな疑問に答えるところだよ。\n\n[half][[5]]なにか訊きたいことはあるかな？"
    --
    AYATEMPLATE.MenuItem('ここはどんなお店？', 'FAQ_AboutTheCafe', 150)
    --
    AYATEMPLATE.MenuItem('亜人ってなに？', 'FAQ_WhatIsAjin')
    --
    AYATEMPLATE.MenuItem('悠梨について', 'FAQ_AboutYuri')
    --
    AYATEMPLATE.MenuItem('鈴について', 'FAQ_AboutSuzu')
    --
    AYATEMPLATE.MenuItem('他の店員さんについて', 'FAQ_AboutOtherCharacter')
    --
    AYATEMPLATE.MenuItem('ユーザからの質問', 'FAQ_FromUserTop', 150)
    --
    AYATEMPLATE.MenuItem('閉じる', 'FAQ_CloseFAQ', 150)
}

FAQ_TopQuick
{
    SHIORI3FW.ResetAITalkInterval

    Init
    --
    "\_q\u[[1000]]\h[[5]]ここは%(username)が私たちに対して思ってそうな疑問に答えるところだよ。%(NewLine(3,0))なにか訊きたいことはあるかな？\_q"
    --
    AYATEMPLATE.MenuItem('ここはどんなお店？', 'FAQ_AboutTheCafe', 150)
    --
    AYATEMPLATE.MenuItem('亜人ってなに？', 'FAQ_WhatIsAjin')
    --
    AYATEMPLATE.MenuItem('悠梨について', 'FAQ_AboutYuri')
    --
    AYATEMPLATE.MenuItem('鈴について', 'FAQ_AboutSuzu')
    --
    AYATEMPLATE.MenuItem('他の店員さんについて', 'FAQ_AboutOtherCharacter')
    --
    AYATEMPLATE.MenuItem('ユーザからの質問', 'FAQ_FromUserTop', 150)
    --
    AYATEMPLATE.MenuItem('閉じる', 'FAQ_CloseFAQ', 150)
}

FAQ_AboutTheCafe
{
    Init
    --
    "\0[[16]]ん。\w5[[0]]ここは保護猫カフェ「アダプト」。\n[[55]]行き先のない猫たちを保護したり、里親さんと巡り合わせたりするお店。/
    \n\n[half][[10]]そういう普通の保護猫カフェでもあるけれど。[[0]]ウチのお店がちょっと特別なのは、亜人たちも保護したり人間の社会に繋げたりしてるところかな。/
    \n[[25]]店員さんもだいたいは猫の亜人なんだよ。/
    \u[[1016]]あなたが遊びに来て、猫たちを人に慣れさせて、あなたが亜人のことを知り、亜人が人間のことを知る。/
    \n[[1000]]ここはそういうお店なの。\n[[1005]]だから、気軽に遊びにきてくれるとうれしいな。\h"
    --
    FAQ_Footer
}

FAQ_WhatIsAjin
{
    Init
    --
    "\0[[0]]人じゃない動物が人の姿になったもの、かな。\n[[6]]擬人化ってやつ？\n[[55]]もともとは私や鈴もふつうの猫だったんだよ。/
    \1[[1006]]人間になりたいって思いが強い子たちが亜人になりやすいらしいけれど、あんまり詳しいことは分かってないの。/
    \0[[56]]人間になりたい子を人間にしてくれるのはいいんだけど、姿が人間になってもみんな人間の社会についてはほとんど知らないの。/
    \n[[0]]そういう子たちもウチのお店でサポートしてるんだ。/
    \n\n[half][[5]]このお店に来てくれれば、きっと%(username)も私たちについて分かってくれると思うな。"
    --
    FAQ_Footer
}

FAQ_AboutYuri
{
    Init
    --
    "\0[[11]]私？　\w8[[16]]私は悠梨、普通の猫だよ。/
    \n[[0]]野良猫からヒトの姿になって、もう4〜5年くらいここで働いてるの。\n[[55]]一応、亜人たちの中では一番古くからいる店員だよ。/
    \1[[1055]]猫なのに度を超えた真面目さで、よく他の猫たちのおもちゃになっているわね。/
    \0………[[54]]随分なご紹介をアリガトウゴザイマス。/
    \1[[1016]]おばかだけど素直で良い子よ。[[1005]]とっても。\n[[1000]]ひとと喋るのが大好きだから、是非お話ししてあげてちょうだい。/
    \0[[16]]ん。\w5[[0]]よかったら、ぜひに。"
    --
    FAQ_Footer
}

FAQ_AboutSuzu
{
    Init
    --
    "\0[[50]]鈴、ご指名ですよ。/
    \1[[1016]]あなたのかわいい猫ちゃん、鈴と申します。\n[[1005]]わたしのことは好きにしてくれていいからね？？/
    \0………[[66]]なんの紹介にもなってませんね。\n[[34]]でも、こういう子です。[[6]]目の前の相手によって態度を水のように柔軟に変化させる女の子。/
    \n[[4]]%(username)、注意してくださいね。/
    \1[[1020]]いいんだよ、見たいように見れば。\n[[1016]]応えてほしいように応えるから。/
    \0………[[13]]重ねてになりますが、ガチ恋などしないように、ね。"
    --
    FAQ_Footer
}

FAQ_AboutOtherCharacter
{
    Init
    --
    "\0[[0]]ウチにはいろんな人がいますよ。[[55]]オーナーと経理の人は人間だけど、他はみんな亜人です。\n[[34]]なんというか、癖の強い人が多くて大変デス……/
    \1[[1058]]働かなかったり、働けなかったり、サボり魔さんも多いものね。/
    \0[[86]]あなたもややサボり魔でしょうに。\w5\n[[0]]他の店員さんについては、ここで全部話すと長くなるので、[[5]]またいつかお話ししますね。"
    --
    FAQ_Footer
}

FAQ_FromUserTop
{
    temp_ChoiseExRef = 0

    Init
    --
    "\h[[16]]\u[[1020]]お客さんからの質問に答えるコーナーだよ。/
    \n[[1055]]訊きたいことがあったら気軽に送ってね。"
    --
    FAQ_UserFAQList
}

FAQ_UserFAQList
{
    _pageIndex = temp_ChoiseExRef
    _isLastPage = 0
    _maxIndex = (_pageIndex + 1) * QUESTIONS_ON_A_PAGE
    if _maxIndex >= FAQ_USER_QUESTION_COUNT{
        _maxIndex = FAQ_USER_QUESTION_COUNT
        _isLastPage = 1
    }

    _list = "\_q\h【ユーザからの質問】%(NewLine(1,0))\_q"
    
    for _i = _pageIndex * QUESTIONS_ON_A_PAGE; _i < (_pageIndex + 1) * QUESTIONS_ON_A_PAGE; _i++{
        if _i < _maxIndex{
            _list += AYATEMPLATE.MenuItem(GetFAQData(_i, FAQ_HEAD), 'FAQ_UserFAQAnswer', _i, 100)
        }
        else{
            _list += NewLine(2,0)
        }
    }

    if FAQ_USER_QUESTION_COUNT > QUESTIONS_ON_A_PAGE{
        _list += NewLine(3,0)

        if _pageIndex == 0 {
            _list += '\_q　　　　　　　　　'
        }
        else{
            _list += "\_q\![*]\q[前のページへ,FAQ_UserFAQList,%(_pageIndex - 1)]　　"
        }

        if _isLastPage == 0{
            _list += "\![*]\q[次のページへ,FAQ_UserFAQList,%(_pageIndex + 1)]"
        }
    }

    _list
    --
    AYATEMPLATE.MenuItem('質問を送る', 'FAQ_AskToGhost', 200)
    --
    FAQ_Footer
}

FAQ_UserFAQAnswer
{
    _faqID = _argv[0]
    _pageIndex = _argv / QUESTIONS_ON_A_PAGE

    Init
    --
    "\0【%(GetFAQData(_faqID, FAQ_QUESTION))】\n\n\w5"
    --
    GetFAQData(_faqID, FAQ_ANSWER)
    --
    '\h'
    --
    AYATEMPLATE.MenuItem('ユーザからの質問に戻る', 'FAQ_UserFAQList', _pageIndex, 150)
    --
    AYATEMPLATE.MenuItem('質問TOPに戻る', 'FAQ_TopQuick', 150)
    --
    AYATEMPLATE.MenuItem('閉じる', 'FAQ_CloseFAQ')
}

FAQ_AskToGhost
{
    Init
    --
    "\0[[55]]はいな。/
    [[25]]なんでも気軽に訊いてくださいね。/
    \1[[1016]]長い文章を送りたい場合はGoogleフォームから送ると楽だよ。/
    \0[[0]]あて先は同じなのでお好きなほうをどうぞ。\![open,inputbox,FAQ_OnInputAsk,0,]"
    --
    AYATEMPLATE.MenuItem('質問フォームをブラウザで開く','FAQ_OpenGoogleForm', 150)
    --
    FAQ_Footer
}

FAQ_OpenGoogleForm
{
    "\C\![open,browser,https://forms.gle/yKZmNFWR2e9nhTKA9]\e"
}

FAQ_OnInputAsk
{
	if (_argv[0] == INPUT_CLOSE) || (_argv[0] == INPUT_TIMEOUT) || (_argv[0] == '') || (_argv[0] == username) {
		'\e'
		return
	}

    Init
    --
    "\0[[6]]メッセージ、送信中………\![enter,onlinemode]\![execute,http-post,https://docs.google.com/forms/u/0/d/e/1FAIpQLSfJVBnhpz0uiZCAJHUMDEQpaxvEMxow2gYpXOvdLdN86_Fcrw/formResponse,--async=OnPostedToForm,--header=mode:no-cors,--param=entry.348543169=%(_argv[0])]"
}

OnPostedToForm
{
    "\0[[25]]\![leave,onlinemode]ん！　送信成功です。/
    \n[[16]]回答までしばらくお待ちくださいね。\e"
}

OnPostedToFormFailure
{
    "\0[[56]]\![leave,onlinemode]送信に失敗しました………/
    \n[[3]]ネットワークの設定を見直してみてください。\e"
}

FAQ_Footer
{
    AYATEMPLATE.MenuItem('質問TOPに戻る', 'FAQ_TopQuick', 200)
    --
    AYATEMPLATE.MenuItem('閉じる', 'FAQ_CloseFAQ')
}

FAQ_CloseFAQ
{
    '\e'
}