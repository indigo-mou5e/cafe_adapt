//******************************************************************************
// 種々のイベント辞書
//******************************************************************************

// 自動でトーク中の改行にウェイトを追加
#define \n[half] %(NewLine(1))
#define \n %(NewLine(2))

// 最初のスコープ変更以外に改行とウェイトを追加
#define '\0 '\h
#define '\1 '\u
#define "\0 "\h
#define "\1 "\u
#define \0 %(NewLine(3))\0
#define \1 %(NewLine(3))\1

#define [[ \s[
#define ]] ]

//******************************************************************************
//シェル切り替え
//******************************************************************************

//------------------------------------------------------------------------------
//シェルチェンジ開始
//------------------------------------------------------------------------------

OnShellChanging
{
	Init
	--

	case dicMode {
		when MODE_NORMAL {
			"\1[[1055]]うん、お着替えしてくるね。/
			\0[[16]]%(username)、少々お待ちください。\e"

			"\1[[1000]]お着替えの時間です。/
			\0[[55]]猫と戯れながらお待ちください。\e"
		}
		when MODE_YURI {
			"\0[[16]]お着替えしてきますね。\n少々お待ちを……\e"
		}
		when MODE_SUZU {
			"\1[[1055]]うん、いいよ。\n[[1005]]それじゃあ着替えてくるね？\e"
		}
	}
}

//------------------------------------------------------------------------------
//シェルチェンジ完了
//------------------------------------------------------------------------------
OnShellChanged
{
	Init
	--
	_escaped_name = SHIORI3FW.EscapeAllTags(reference[0])

	case _escaped_name {
		when 'master' {
			case dicMode{
				when MODE_NORMAL{
					"\1[[1000]]お待たせ！\nいつもの鈴ちゃんだよ。/
					\0[[16]]やっぱりこの格好が一番落ち着きますね。\e"
				}
				when MODE_YURI{
					'\0[[16]]ん。いつもの格好は落ち着きますね。\e'
				}
				when MODE_SUZU{
					"\1[[1000]]お待たせ！\nいつもの鈴ちゃんだよ。\e"
				}
			}
		}
		others {
			case dicMode{
				when MODE_NORMAL{
					"\0[[16]]お待たせしました。/
					\1[[1005]]どうかな、似合ってる？？\e"
				}
				when MODE_YURI{
					'\0[[0]]お待たせ！　どうかな？？\e'
				}
				when MODE_SUZU{
					"\1[[1000]]着替えてきたよ。\nどう、かわいい？？\e"
				}
			}
		}
	}
}

//******************************************************************************
//インストール関連
//******************************************************************************

//------------------------------------------------------------------------------
//インストール中のメッセージ
//------------------------------------------------------------------------------
OnInstallBegin
{
	Init
	--
	if dicMode != MODE_SUZU
	{
		'\0[[16]]インストール中です………\e'
	}
	else{
		'\1[[1016]]インストール中です………\e'
	}
}

//------------------------------------------------------------------------------
//インストール完了メッセージ
//------------------------------------------------------------------------------
OnInstallComplete
{
	_ref0 = SPLIT(reference[0], ' with', 2)[0] //hogehoge with hemohemo

	if dicMode != MODE_SUZU{
	'\0\s[25]'
	}
	else{
	'\1\s[1005]'	
	}
	--
	if _ref0 == 'shell' {
		'シェル'
	}
	elseif _ref0 == 'ghost' {
		'ゴースト'
	}
	elseif _ref0 == 'balloon' {
		'バルーン'
	}
	elseif _ref0 == 'plugin' {
		'プラグイン'
	}
	--
	_escaped_name = SHIORI3FW.EscapeAllTags(reference[1])
	"「%(_escaped_name)」のインストールが完了しました。\n"
	--
	if dicMode != MODE_SUZU{
	'\s[0]'
	}
	else{
	'\s[1000]'	
	}
	--
	if basewarenameex == 'SSP' {
		_id = ''
		if _ref0 == 'shell' {
			_id = 'shell'
		}
		elseif _ref0 == 'ghost' {
			_id = 'ghost'
		}
		elseif _ref0 == 'balloon' {
			_id = 'balloon'
		}

		if _id != '' {
			'切り替えますか？'
			--
			AYATEMPLATE.MenuItem('切り替える', 'OnAYATEMPLATE.InstallChange', _id, 200)
			--
			AYATEMPLATE.MenuItem('切り替えない', 'Yen_E')
		}
	}
}

OnAYATEMPLATE.InstallChange
{
	"\![change,%(AYATEMPLATE.EscapeText(reference[0])),lastinstalled]"
}

//------------------------------------------------------------------------------
//インストール失敗メッセージ
//------------------------------------------------------------------------------
OnInstallFailure
{
	_escaped_message = SHIORI3FW.EscapeAllTags(reference[0])

	Init
	--
	case dicMode{
		when MODE_NORMAL{
			"\0\s[4]インストールに失敗しました……/
			\1[[1004]]原因は以下の通りみたいね。%(NewLine(3,0))/
			\_q「%(_escaped_message)」\_q\e"
		}
		when MODE_YURI{
			"\0\s[4]インストールに失敗しました……\n/
			[[56]]原因は以下の通りみたいです。%(NewLine(3,0))/
			\_q「%(_escaped_message)」\_q\e"
		}
		when MODE_SUZU{
			"\1[[1056]]インストールに失敗したみたい。\n/
			[[1004]]原因は以下の通りみたいね。%(NewLine(3,0))/
			\_q「%(_escaped_message)」\_q\e"
		}
	}
}

//------------------------------------------------------------------------------
//インストール拒否（他のゴーストのためのアーカイブだった）
//------------------------------------------------------------------------------
OnInstallRefuse
{
	_escaped_name = SHIORI3FW.EscapeAllTags(reference[0])

	Init
	--
	case dicMode{
		when MODE_NORMAL{
			"\0\s[11]ありゃ。\n[[6]]これは%(_escaped_name)さん用のアーカイブみたい。/
			\1\s[1016]本人に直接渡してあげてね。\e"
		}
		when MODE_YURI{
			"\0\s[11]ありゃ。\n[[6]]これは%(_escaped_name)さん用のアーカイブみたい。\n/
			\s[16]本人に直接渡してあげてね。\e"
		}
		when MODE_SUZU{
			"\1[[1006]]ん。これは%(_escaped_name)さん用のアーカイブみたいね。\n/
			\s[1000]本人に直接渡してあげてね。\e"
		}
	}
}

//******************************************************************************
//消滅関連
//******************************************************************************

//------------------------------------------------------------------------------
//消滅指示
//------------------------------------------------------------------------------
OnVanishSelecting
{
	Init
	--
	"\u[[1013]]\h[[3]]えっ？\n%(username)、遠くに行っちゃうの？/
	\1\s[1013]ざんねん。寂しくなるわね。\e"

}

//------------------------------------------------------------------------------
//消滅開始
//------------------------------------------------------------------------------
OnVanishSelected
{
	Init
	--
	"\u[[1006]]\h\s[56]………。\n[[3]]そっか。またいつか会えるといいですね。/
	\1[[1055]]どうかお幸せにね。\n[[1005]]バイバイ。/
	\0[[5]]さようなら、お元気で。\e"
}

//------------------------------------------------------------------------------
//消滅中止
//------------------------------------------------------------------------------
OnVanishCancel
{
	Init
	--
	"\u[[1055]]\h\s[14]はあ、冗談でしたか……/
	\1[[1005]]よかった。\nまだしばらく楽しみましょうね？/
	\0[[6]]ん。\w6[[0]]引き続きよろしくお願いしますね、%(username)。\e"
}

//------------------------------------------------------------------------------
//他のゴーストが消滅して自分に切り替わった
//------------------------------------------------------------------------------
OnVanished
{
	_escaped_name = SHIORI3FW.EscapeAllTags(reference[0])
	dicMode = MODE_NORMAL

	Init
	--
	"\0\s[13]%(_escaped_name)さんが居なくなっちゃった。/
	\1\s[1006]いつかは別れる日も来るわよ。\n[[1005]]せめてそれまで、楽しみましょう？\e"

	"\1\s[1006]%(_escaped_name)さん、お疲れ様でした。/
	\0[[56]]………。/
	\1[[1098]]悲しそうな顔しない。\nお仕事するわよ。/
	\0[[54]]はい、がんばります……\e"
}

//******************************************************************************
//メールチェック
//******************************************************************************

//------------------------------------------------------------------------------
//メールチェック開始
//------------------------------------------------------------------------------
OnBIFFBegin
{
	Init
	--
	case dicMode{
		when MODE_NORMAL{
			'\h[[16]]\u\s[1016]'
		}
		when MODE_YURI{
			'\h\s[16]'
		}
		when MODE_SUZU{
			'\u\s[1016]'
		}
	}
	--
	'メールチェックを開始します。\e'
}

//------------------------------------------------------------------------------
//メールチェック成功
//------------------------------------------------------------------------------
OnBIFFComplete
{
	Init
	--
	case dicMode{
		when MODE_NORMAL{
			'\h[[16]]\u\s[1000]'
		}
		when MODE_YURI{
			'\h\s[0]'
		}
		when MODE_SUZU{
			'\u\s[1000]'
		}
	}

	if TOINT(reference[0]) == 0 {
		//---- メールは無かった
		"チェック完了。\n残念ながら、新着メールはなかったみたい。\e"
	}
	else {
		//---- メールが届いていた
		if dicMode != MODE_YURI{
			"チェック完了。\n%(reference[0])通のメールが届いているみたい。\s[1016]見てみるといいんじゃないかしら。\e"
		}
		else{
			"チェック完了。\n%(reference[0])通のメールが届いているみたい。\s[16]確認してみたら？\e"
		}
	}
}

//------------------------------------------------------------------------------
//メールチェック失敗
//------------------------------------------------------------------------------
OnBIFFFailure
{
	Init
	--
	case dicMode{
		when MODE_NORMAL{
			"\h[[16]]\u[[1056]]"
		}
		when MODE_YURI{
			'\h[[56]]'
		}
		when MODE_SUZU{
			"\u[[1056]]"
		}
	}
	--
	"メールチェックに失敗しちゃいました。\n\n[half]"
	--
	if dicMode != MODE_YURI{
		'\u[[1004]]'
	}
	else{
		'\h[[4]]'
	}
	--
	if reference[0] == 'timeout' {
		//---- タイムアウト
		'接続がタイムアウトしたみたい。\e'
	}
	elseif reference[0] == 'kick' {
		//---- 蹴られた
		'キックされちゃったみたい。\e'
	}
	elseif reference[0] == 'defect' {
		//---- 設定誤り
		"メールチェックの設定がどうもおかしいみたい。\n設定を見直してみてね。\e"
	}
	else {
		//---- 原因不明
		'原因不明。\e'
	}
}

//******************************************************************************
//ネットワーク更新
//******************************************************************************

//------------------------------------------------------------------------------
//ネットワーク更新開始
//------------------------------------------------------------------------------
OnUpdateBegin
{
	Init
	--
	if dicMode != MODE_SUZU{
		'\0[[55]]お届け物が届いてないか確認するね。\e'
		'\0[[35]]お届け物の確認の時間だ！！\e'
		'\0[[16]]素敵なお届け物、届いてないかしら。\e'
	}
	else{
		'\1[[1016]]お届け物がないか確認してくるね。\e'
	}
}

//------------------------------------------------------------------------------
//ネットワーク更新準備完了
//------------------------------------------------------------------------------
OnUpdateReady
{
	Init
	--
	if dicMode != MODE_SUZU{
		"\0\s[5]%(TOINT(reference[0])+1)個のお届け物がありました！\e"
	}
	else{
		"\1\s[1005]%(TOINT(reference[0])+1)個のお届け物が届いてるみたい。\e"
	}
}

//------------------------------------------------------------------------------
//ネットワーク更新終了
//------------------------------------------------------------------------------
OnUpdateComplete
{
	Init
	--
	if reference[0] == 'none' {
		//---- 更新すべきファイルが無かった
		case dicMode{
			when MODE_NORMAL{
				"\0[[4]]……お届け物、何もありませんでした。/
				\1[[1006]]お疲れ様。"

				"\0[[56]]まだ何も届いてなかったみたいです。/
				\1[[1050]]そう。果報はゆっくり待ちましょう。"
			}
			when MODE_YURI{
				"\0[[4]]……お届け物、何もありませんでした。"

				"\0[[56]]まだ何も届いてなかったみたいです。"
			}
			when MODE_SUZU{
				"\1[[1006]]なにも届いてなかったみたい。\n[[1004]]残念ね。"

				"\1[[1006]]まだなにもないみたいね。\n[[1055]]果報はゆっくり待ちましょう？"
			}
		}
	}
	else {
		//---- 実際に更新が完了した
		case dicMode{
			when MODE_NORMAL{
				"\0\s[25]お届け物、受領完了！！/
				\1\s[1016]ん、おつかれさま。"

				"\0\s[0]ん、これで全部かな。/
				\1\s[1016]これでまた新しいトークができるわね。/
				\0[[54]]メタ発言はおやめになっていただいて。"

				"\0\s[5]終わりー！！/
				\1[[1050]]ありがと。\n何が届いたのかしら。"
			}
			when MODE_YURI{
				"\0\s[25]お届け物、受領完了！！"

				"\0\s[0]ん、これで全部かな。\n[[16]]お疲れ様、私！！"
			}
			when MODE_SUZU{
				"\1[[1006]]ふう。\w5\n[[1000]]これで全部みたいね。\n\n[half][[1005]]褒めてもいいんだよ？？"

				"\1[[1016]]終わったみたい。[[1005]]なにが届いたのかな？？"
			}
		}
	}
	--
	AYATEMPLATE.MenuItem('更新履歴を見る','OpenVersionHistory',150)
	--
	AYATEMPLATE.MenuItem('閉じる','Yen_E')
	--
	'\e'
}

//------------------------------------------------------------------------------
//ネットワーク更新失敗
//------------------------------------------------------------------------------
OnUpdateFailure
{
	Init
	--
	if dicMode != MODE_SUZU{
		"\0[[56]]お届け物、受け取れませんでした……\n\n[half]"
	}
	else{
		"\1[[1056]]お届け物が受け取れなかったみたい。\n\n[half]"
	}
	--
	if reference[0] == 'timeout' {
		//---- タイムアウトした
		if dicMode != MODE_YURI{
			'\1[[1004]]接続がタイムアウトしちゃったみたい。\e'
		}
		else{
			'\0[[4]]接続がタイムアウトしたみたいです。\e'
		}
	}
	elseif reference[0] == 'md5 miss' {
		//---- MD5値が一致しなかった
		if dicMode != MODE_YURI{
			"\u\s[1004]MD5値が一致しなかったみたい。\nお手数だけれど、配布元に訊ねてみて。\e"
		}
		else{
			"\h\s[4]MD5値が一致しなかったみたいです。\nお手数ですが、配布元に訊ねてみてください。\e"
		}
	}
	else {
		//---- 上記以外の理由で失敗した
		_escaped_message = SHIORI3FW.EscapeAllTags(reference[0])
		if dicMode != MODE_YURI{
			"\1\s[[1004]]エラーメッセージは以下の通りよ。\n\n[half]\_q「%(_escaped_message)」\_q\e"
		}
		else{
			"\0\s[[4]]エラーメッセージは以下の通りです。\n\n[half]\_q「%(_escaped_message)」\_q\e"
		}
	}
}

//------------------------------------------------------------------------------
//ネットワーク更新 ダウンロード開始
//------------------------------------------------------------------------------
/* // この辺、トークを加えるとネットワーク更新に時間がかかるので全消し中。
OnUpdate.OnDownloadBegin
{
	SHIORI3FW.ResetAITalkInterval
	_escaped_name = SHIORI3FW.EscapeAllTags(reference[0])
	
	Init
	--
	if dicMode != MODE_SUZU{
		'\0\s[16]'
	}
	else{
		'\1\s[1016]'
	}
	--
	"\_qお届け物「%(_escaped_name)」を受け取り中………\_q\e"
}

//------------------------------------------------------------------------------
//ネットワーク更新 MD5値照合開始
//------------------------------------------------------------------------------
OnUpdate.OnMD5CompareBegin
{
	Init
	--
	if dicMode != MODE_SUZU{
		'\0\s[6]'
	}
	else{
		'\1\s[1006]'
	}
	--
	'\_q中身を確認中……\_q\e'
}

//------------------------------------------------------------------------------
//ネットワーク更新 MD5値一致
//------------------------------------------------------------------------------
OnUpdate.OnMD5CompareComplete
{
	Init
	--
	if dicMode != MODE_SUZU{
		'\_q\h\s[55]ん、オッケー。\_q\e'
	}
	else{
		'\_q\u\s[1055]うん、大丈夫ね。\_q\e'
	}
}
*/

//------------------------------------------------------------------------------
//ネットワーク更新 MD5値不一致
//------------------------------------------------------------------------------
OnUpdate.OnMD5CompareFailure
{
	Init
	--
	if dicMode != MODE_SUZU{
		"\0\s[4]あ、ダメっぽい……\_q\n\n[half]（MD5値が一致しません。\n　製作者にお問い合わせ願います。）\_q\e"
	}
	else{
		"\1\s[1004]あれ、なにか違うかも……？\_q\n\n[half]（MD5値が一致しません。\n　製作者にお問い合わせ願います。）\_q\e"
	}
}

//******************************************************************************
//ヘッドラインセンス
//******************************************************************************

// ToDo: だるいからほぼサンプルゴーストのまま。いつかやる。

//------------------------------------------------------------------------------
//ヘッドラインセンス開始
//------------------------------------------------------------------------------
OnHeadlinesenseBegin
{
	_escaped_name = SHIORI3FW.EscapeAllTags(reference[0])
	
	Init
	--
	if dicMode != MODE_SUZU{
		'\0\s[0]'
	}
	else{
		'\1\s[1000]'
	}
	--
	"%(_escaped_name)ヘッドライン取得中。\e"
}

//------------------------------------------------------------------------------
//ヘッドライン読み上げ
//------------------------------------------------------------------------------
OnHeadlinesense.OnFind
{
	_escaped_name = SHIORI3FW.EscapeAllTags(reference[0])

	Init
	--
	if dicMode != MODE_SUZU{
		'\0\s[0]'
	}
	else{
		'\1\s[1000]'
	}
	--
	//---- はじめのセリフ
	if reference[2] == 'First' || reference[2] == 'First and Last' {
		//---- 最初のページ
		"\b2%(_escaped_name)のヘッドラインです。"
	}
	elseif reference[2] == 'Next' {
		//---- 真ん中あたりのページ
		"\b2%(_escaped_name)ヘッドライン、続き。"
	}
	elseif reference[2] == 'Last' {
		//---- 最後のページ
		"\b2%(_escaped_name)ヘッドライン、これが最後のページです。"
	}
	--
	//---- ヘッドライン本体
	"\n\n%(reference[3])\n"
	--
	//---- 選択肢
	if reference[2] == 'First' || reference[2] == 'Next' {
		//---- 次のページがある場合
		AYATEMPLATE.MenuItem('次のページ', '')
		--
		AYATEMPLATE.MenuItem('サイトへ', reference[1])
		--
		AYATEMPLATE.MenuItem('やめ', 'Menu_HEADLINECANCEL', 150)
	}
	else {
		//---- 次のページがない場合
		AYATEMPLATE.MenuItem('サイトへ', reference[1])
		--
		AYATEMPLATE.MenuItem('やめ', 'Menu_HEADLINECANCEL', 150)
	}
}

//------------------------------------------------------------------------------
//ヘッドライン更新が無かった
//------------------------------------------------------------------------------
OnHeadlinesenseComplete
{
	_escaped_name = SHIORI3FW.EscapeAllTags(reference[0])

	Init
	--
	if dicMode != MODE_SUZU{
		'\0\s[0]'
	}
	else{
		'\1\s[1000]'
	}
	--
	"%(_escaped_name)、更新はないみたいです。\e"
}

//------------------------------------------------------------------------------
//ヘッドラインセンス失敗
//------------------------------------------------------------------------------
OnHeadlinesenseFailure
{
	_escaped_name = SHIORI3FW.EscapeAllTags(reference[0])
	
	Init
	--
	if dicMode != MODE_SUZU{
		'\0\s[0]'
	}
	else{
		'\1\s[1000]'
	}
	--
	"ヘッドラインセンスに失敗しました。\n\n理由："
	--
	if reference[0] == "can't download" {
		'ダウンロード失敗。'
	}
	elseif reference[0] == "can't analyze" {
		'ヘッドライン解析失敗。'
	}
	else {
		'謎。'
	}
	--
	'\e'
}

//******************************************************************************
//ファイル作成関連
//******************************************************************************

//------------------------------------------------------------------------------
//updates2.dau作成開始
//------------------------------------------------------------------------------
OnUpdatedataCreating
{
	Init
	--
	if dicMode != MODE_SUZU{
		'\0\s[6]アップデートデータ作成中デス。\e'
	}
	else{
		'\1\s[1006]アップデートデータを作成するね。\e'
	}
}

//------------------------------------------------------------------------------
//updates2.dau作成完了
//------------------------------------------------------------------------------
OnUpdatedataCreated
{
	Init
	--
	if dicMode != MODE_SUZU{
		'\0\s[5]ん。updates2.dau、作成完了！\e'
	}
	else{
		'\1\s[1005]OK。updates2.dau、作成できたよ。\e'
	}
}

//------------------------------------------------------------------------------
//NAR作成開始
//------------------------------------------------------------------------------
OnNarCreating
{
	Init
	--
	if dicMode != MODE_SUZU{
		'\0\s[6]narファイルを作成します。\e'
	}
	else{
		'\1\s[1006]narファイルを作成するね。\e'
	}
}

//------------------------------------------------------------------------------
//NAR作成完了
//------------------------------------------------------------------------------
OnNarCreated
{
	Init
	--
	if dicMode != MODE_SUZU{
		"\0\s[5]ん、終わり。\n\_q「%(SHIORI3FW.EscapeAllTags(reference[0]))」\_qのnarファイルを\_q「%(SHIORI3FW.EscapeAllTags(reference[1]))」\_qに詰め込みました。\e"
	}
	else{
		"\1\s[1005]終わったよ。\n\_q「%(SHIORI3FW.EscapeAllTags(reference[0]))」\_qのnarファイルを\_q「%(SHIORI3FW.EscapeAllTags(reference[1]))」\_qに置いておいたよ。\e"
	}
}

//******************************************************************************
//時計合わせ
//みか (http://homepage2.nifty.com/ko-ki/) の辞書を参考にしました。感謝＞こーき氏
//******************************************************************************

//------------------------------------------------------------------------------
//時計合わせ開始
//------------------------------------------------------------------------------
OnSNTPBegin
{
	Init
	--
	case dicMode{
		when MODE_NORMAL{
			'\0\s[6]'
		}
		when MODE_YURI{
			'\u[[1999]]\h[[6]]'
		}
		when MODE_SUZU{
			'\h[[999]]\u[[1006]]'
		}
	}
	--
	"%(SHIORI3FW.EscapeAllTags(reference[0]))から正しい時刻を取得中……\e"
}

//------------------------------------------------------------------------------
//時刻取得成功、比較開始
//この関数で 2008/03/25 19:15:05 のように見やすい形に作り変えます。
//詳しくはSTRFORM関数の説明を見てね！
//わからなければこのままで。
//------------------------------------------------------------------------------

SNTPCompare_StrForm
{
	_txt = SPLIT(_argv[0], ',')
	STRFORM('$04d/$02d/$02d $02d:$02d:$02d', TOINT(_txt[0]), TOINT(_txt[1]), TOINT(_txt[2]), TOINT(_txt[3]), TOINT(_txt[4]), TOINT(_txt[5]))
}

OnSNTPCompare
{
	Init
	--
	case dicMode{
		when MODE_NORMAL{
			'\0\s[76]'
		}
		when MODE_YURI{
			'\u[[1999]]\h[[76]]'
		}
		when MODE_SUZU{
			'\h[[999]]\u[[1005]]'
		}
	}
	--
	"\_q/
	修正前 %(SNTPCompare_StrForm(reference[2]))%(NewLine(2,0))/
	修正後 %(SNTPCompare_StrForm(reference[1]))%(NewLine(2,0))/
	\_q%(NewLine(3,0))"
	--
	if reference[3] == 0 {
		if dicMode != MODE_SUZU{
			'うーん、ぴったりデスね……\e'
		}
		else{
			'ん。時計のズレはないみたい。\e'
		}
	}
	else {
		//\6は補正実行タグ。消しちゃだめ
		if dicMode != MODE_SUZU{
			"\6\h\s[5]%(SHIORI3FW.EscapeAllTags(reference[3]))秒のずれを修正しました。\e"
		}
		else{
			"\6\u\s[1005]%(SHIORI3FW.EscapeAllTags(reference[3]))秒ズレてたみたい。\n修正しておいたよ。\e"
		}
	}
}

//------------------------------------------------------------------------------
//時計合わせ失敗
//------------------------------------------------------------------------------
OnSNTPFailure
{
	Init
	--
	if dicMode != MODE_SUZU{
		"\0\s[4]……時計合わせ失敗。\n[[56]]サービスに繋がらないようです。\e"
	}
	else{
		"\1\s[1013]時計合わせ失敗。\n[[1056]]サービスに繋がらないみたい。\e"
	}
}

//******************************************************************************
//そのほか
//******************************************************************************

//------------------------------------------------------------------------------
//ディスプレイ解像度変更
//------------------------------------------------------------------------------
OnDisplayChange
{
	//---- 変数にディスプレイ解像度と色深度に関する情報を取得
	//---- 使うかどうかは分からないがとりあえず保持しておく
	displaybpp = reference[0]
	displaywidth = reference[1]
	displayheight = reference[2]
	SHIORI3FW.RegisterTempVar('displaybpp', 'displaywidth', 'displayheight')
}

//------------------------------------------------------------------------------
//URLドロップ
//CROW でゴーストインストーラからゴーストをインストールを始めた時にも呼ばれます
//------------------------------------------------------------------------------
OnURLDropping
{
	Init
	--
	if dicMode != MODE_SUZU{
		"\0\s[6]%(SHIORI3FW.EscapeAllTags(reference[0]))からデータを取得しています。\e"
	}
	else{
		"\1\s[1016]%(SHIORI3FW.EscapeAllTags(reference[0]))からデータを取得中。\e"
	}
}

//------------------------------------------------------------------------------
//起動時に MATERIA を検出
//------------------------------------------------------------------------------
/*
OnEmbryoExist
{
"\0\s[0]あれ、「伺か」が起動してるよ。\w8\1ありがたやありがたや。\w8\0\s[4]\n\nなんで拝むの？\e"
}
*/

//------------------------------------------------------------------------------
//起動時に猫どりふを検出
//------------------------------------------------------------------------------
/*
OnNekodorifExist
{
"\0\s[2]うわ、猫どりふだ‥\w5‥\w5\w8\1\s[1002]まだあったのか！\w8\0\s[4]\n\nえ、そこが問題なの？\e"
}
*/

//------------------------------------------------------------------------------
//スクリーンセーバー実行開始
//------------------------------------------------------------------------------
/*
OnScreenSaverStart
{
"\0\s[5]画面の焼き付きを保護する開始っ！\w8\e"
}
*/

//------------------------------------------------------------------------------
//スクリーンセーバー実行終了
//------------------------------------------------------------------------------
OnScreenSaverEnd
{
	Init
	--
	case dicMode{
		when MODE_NORMAL{
			"\1\s[1000]あ、おはよう。/
			\0[[8]]よく寝てたから起こさないでおきましたよ。\e"

			"\0[[10]]ん、仮眠終了ですか？/
			\1[[1055]]休んでても作業しててもいいからね？\e"

			"\0[[76]]スクリーンセーバーがなくなったようですね。/
			\1[[1061]]………？？/
			\0[[18]]%(username)、わかりますか。\nこれがスマホ世代です、ジェネレーションギャップです。\e"
		}
		when MODE_YURI{
			"\0[[55]]おはようございます。\n/
			[[8]]よく寝てたから起こさないでおきましたよ。\e"

			"\0[[5]]おかえりなさい。\n/
			[[6]]びっくりしましたよ、急にいなくなるんですから。\e"
		}
		when MODE_SUZU{
			"\1[[1020]]あら、おはよう。\n/
			[[1016]]いいんだよ、寝ていても。[[1005]]好きに過ごしてね？？\e"

			"\1[[1015]]おかえりなさい。\n/
			[[1000]]用事はもういいの？？/e"
		}
	}
}

//******************************************************************************
//最小化、最小化復帰 OnWindowStateRestore
//******************************************************************************
OnWindowStateMinimize
{
	temp_minimizeSecond = currentOperationSecond
}

OnWindowStateRestore
{
	// 最小化していた秒数
	_minimizeTime = currentOperationSecond - temp_minimizeSecond

	Init
	--
	// 復帰1分以内
	if _minimizeTime < 60 {
		if dicMode != MODE_SUZU{
			'\h[[0]]'
			'\h[[6]]'
			'\h[[16]]'
		}
		else{
			'\h[[999]]'
		}
		--
		if dicMode != MODE_YURI{
			'\u[[1000]]\e'
			'\u[[1006]]\e'
			'\u[[1016]]\e'
		}
		else{
			'\u[[1999]]\e'
		}
	}
	// 復帰5分以内
	elseif _minimizeTime < 300 {
		case dicMode{
			when MODE_NORMAL{
				"\u[[1000]]\h[[0]]ん、おかえり。/
				\1[[1016]]もう用事はいいのかな。\e"

				"\h\s[16]\u\s[1000]おかえりなさいませ。/
				\0[[0]]早かったですね。\e"

				"\u\s[1016]\h\s[0]用事は終わりましたか？/
				\1[[1005]]%(username)、おかえりなさい。待ってたよ。\e"

				"\h[[16]]\u[[1005]]%(username)、おかえりなさい。\e"

				"\u[[1055]]\h[[0]]あら、戻ってきましたね。/
				\1[[1005]]またゆっくりお話ししましょう？\e"
			}
			when MODE_YURI{
				"\h[[0]]ん、おかえりなさい。\e"

				"\0[[55]]あら、おかえり。\n[[0]]早かったですね。\e"

				"\0[[11]]あ、%(username)。\n[[0]]用事は終わりましたか？\e"
			}
			when MODE_SUZU{
				"\1[[1020]]おかえりなさいませ、ご主人様？\e"

				"\1[[1005]]%(username)、おかえりなさい。\e"

				"\1[[1005]]おかえり。\n[[1000]]もう用事はいいのかな？\e"
			}
		}
	}
	elseif _minimizeTime < 1200 {
		case dicMode{
			when MODE_NORMAL{
				"\u\s[1016]\h\s[0]用事は終わりましたか？/
				\1[[1005]]%(username)、おかえりなさい。待ってたよ。\e"

				"\h[[16]]\u[[1005]]%(username)、おかえりなさい。\e"

				"\u[[1055]]\h[[0]]あら、戻ってきましたね。/
				\1[[1005]]またゆっくりお話ししましょう？\e"
			}
			when MODE_YURI{
				"\h[[0]]ん、おかえりなさい。\e"

				"\0[[11]]あ、%(username)。\n[[0]]用事は終わりましたか？\e"
			}
			when MODE_SUZU{
				"\1[[1020]]おかえりなさいませ、ご主人様？\e"

				"\1[[1005]]%(username)、おかえりなさい。\e"
			}
		}
	}
	elseif _minimizeTime < 3600 {
		case dicMode{
			when MODE_NORMAL{
				"\h[[10]]\u[[1016]]おかえりなさいませ。/
				\0[[0]]長かったですね。\w7[[5]]待ってましたよ。\e"

				"\u[[1000]]\h[[0]]ん、戻ってきましたね。/
				\1[[1009]]%(username)、おそいよ。\e"
			}
			when MODE_YURI{
				"\h[[11]]あら、ようやく戻ってきましたね。\n[[5]]待ってましたよ。\e"

				"\0[[11]]あ、%(username)。\n[[6]]ずいぶん長かったですね。[[5]]おかえりなさい。\e"
			}
			when MODE_SUZU{
				"\1[[1009]]………おそいよ、%(username)。\e"

				"\1[[1065]]%(username)、おかえりなさい。\n\n[half][[1005]]戻ってきてくれて、うれしいよ。\e"
			}
		}
	}
	else {
		case dicMode{
			when MODE_NORMAL{
				"\h[[10]]\u[[1016]]おかえりなさいませ。/
				\0[[0]]長かったですね。\w7[[5]]待ってましたよ。\e"

				"\u[[1000]]\h[[0]]ん、戻ってきましたね。/
				\1[[1009]]%(username)、おそいよ。\e"

				"\h[[10]]\u[[1005]]お帰りなさいませ、ご主人様？/
				\0[[13]]もう戻ってこないのかと思いましたよ。\e"

				"\u[[1006]]\h[[0]]ん、ようやくお帰りですか。\n\n[half][[58]]鈴なら寝ちゃいましたよ。\nまたすぐ起きるでしょうけどね。\e"
			}
			when MODE_YURI{
				"\h[[11]]あら、ようやく戻ってきましたね。\n[[13]]もう戻ってこないのかと思いましたよ。\e"

				"\0[[11]]あ、%(username)。\n[[6]]ずいぶん長かったですね。[[5]]おかえりなさい。\e"
			}
			when MODE_SUZU{
				"\1[[1009]]………おそいよ、%(username)。\e"

				"\1[[1006]]……………すぅ。\n\n[half]/
				………[[1021]]あ。\w9[[1029]]えへへ、わたしも寝ちゃってたみたい\e"
			}
		}
	}

}

OnNotifyUserInfo
{
	if isReadUserInfoAlready != 1 {
		tempUsername = reference[0]
		_birthDay = SPLIT(reference[2], ',')
		userBirthMonth = TOINT(_birthDay[1])
		userBirthDay = TOINT(_birthDay[2])

		case reference[3] {
			when 'male'
				userGender = 1
			when 'female'
				userGender = 2
			others
				userGender = 0
		}

		isReadUserInfoAlready = 1
	}
}