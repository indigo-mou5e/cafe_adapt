//******************************************************************************
// 種々のイベント辞書
//******************************************************************************

// 自動でトーク中の改行にウェイトを追加
#define \n[half] %(NewLine(1))
#define \n %(NewLine(2))

// 最初のスコープ変更以外に改行とウェイトを追加
#define '\0 '\u\s[1000]\h
#define '\1 '\h\s[0]\u
#define "\0 "\u\s[1000]\h
#define "\1 "\h\s[0]\u
#define \0 %(NewLine(3))\0
#define \1 %(NewLine(3))\1

#define [[ \s[
#define ]] ]

//******************************************************************************
//シェル切り替え
//******************************************************************************

//------------------------------------------------------------------------------
//シェルチェンジ開始
//------------------------------------------------------------------------------

OnShellChanging
{
	Init
	--
	"\1[[1055]]うん、お着替えしてくるね。/
	\0[[16]]%(username)、少々お待ちください。\e"

	"\1[[1000]]お着替えの時間です。/
	\0[[55]]猫と戯れながらお待ちください。\e"
}

//------------------------------------------------------------------------------
//シェルチェンジ完了
//------------------------------------------------------------------------------
OnShellChanged
{
	Init
	--
	_escaped_name = SHIORI3FW.EscapeAllTags(reference[0])
	
	case _escaped_name
	{
		when 'master'{
			"\1[[1000]]お待たせ！\nいつもの鈴ちゃんだよ。/
			\0[[16]]やっぱりこの格好が一番落ち着きますね。\e"
		}
		others{
			"\0[[16]]お待たせしました。/
			\1[[1005]]どうかな、似合ってる？？\e"
		}
	}
}


//******************************************************************************
//インストール関連
//******************************************************************************

//------------------------------------------------------------------------------
//インストール中のメッセージ
//------------------------------------------------------------------------------
OnInstallBegin
{
	"\0[[16]]インストール中です………\e"
}

//------------------------------------------------------------------------------
//インストール完了メッセージ
//------------------------------------------------------------------------------
OnInstallComplete
{
	_ref0 = SPLIT(reference[0],' with',2)[0]//hogehoge with hemohemo

	"\0\s[25]"
	--
	if _ref0 == 'shell' {
		'シェル'
	}
	elseif _ref0 == 'ghost' {
		'ゴースト'
	}
	elseif _ref0 == 'balloon' {
		'バルーン'
	}
	elseif _ref0 == 'plugin' {
		'プラグイン'
	}
	--
	_escaped_name = SHIORI3FW.EscapeAllTags(reference[1])
	"「%(_escaped_name)」のインストールが完了しました。\n"
	--
	if basewarenameex == 'SSP' {
		_id = ''
		if _ref0 == 'shell' {
			_id = 'shell'
		}
		elseif _ref0 == 'ghost' {
			_id = 'ghost'
		}
		elseif _ref0 == 'balloon' {
			_id = 'balloon'
		}

		if _id != '' {
			'[[16]]切り替えますか？'
			--
			AYATEMPLATE.MenuItem("切り替える","OnAYATEMPLATE.InstallChange",_id,200)
			--
			AYATEMPLATE.MenuItem("切り替えない","Menu_CANCEL")
		}
	}
}

OnAYATEMPLATE.InstallChange
{
	"\![change,%(AYATEMPLATE.EscapeText(reference[0])),lastinstalled]"
}

//------------------------------------------------------------------------------
//インストール失敗メッセージ
//------------------------------------------------------------------------------
OnInstallFailure
{
	_escaped_message = SHIORI3FW.EscapeAllTags(reference[0])

	Init
	--
	"\0\s[4]インストールに失敗しました……/
	\1[[4]]原因は以下の通りみたいね。\n「%(_escaped_message)」\e"
}

//------------------------------------------------------------------------------
//インストール拒否（他のゴーストのためのアーカイブだった）
//------------------------------------------------------------------------------
OnInstallRefuse
{
	_escaped_name = SHIORI3FW.EscapeAllTags(reference[0])
	
	Init
	--
	"\0\s[11]あれれ。\n[[36]]これは%(_escaped_name)さん用のアーカイブみたい。/
	\1\s[1016]本人に直接渡してあげてね。\e"
}


//******************************************************************************
//消滅関連
//******************************************************************************

//------------------------------------------------------------------------------
//消滅指示
//------------------------------------------------------------------------------
OnVanishSelecting
{
	Init
	--
	"\0\s[3]えっ？\n%(username)、遠くに行っちゃうの？/
	\1\s[13]ざんねん。寂しくなるわね。\e"
}

//------------------------------------------------------------------------------
//消滅開始
//------------------------------------------------------------------------------
OnVanishSelected
{
	Init
	--
	"\0\s[56]………。\n[[3]]そっか。またいつか会えるといいですね。/
	\1[[1055]]どうかお幸せにね。\n[[1005]]バイバイ。/
	\0[[5]]さようなら、お元気で。\e"
}

//------------------------------------------------------------------------------
//消滅中止
//------------------------------------------------------------------------------
OnVanishCancel
{
	
	Init
	--
	"\0\s[14]はあ、冗談でしたか……/
	\1[[1005]]よかった。\nまだしばらく楽しみましょうね？/
	\0[[6]]ん。\w6[[0]]引き続きよろしくお願いしますね、%(username)。\e"
}

//------------------------------------------------------------------------------
//他のゴーストが消滅して自分に切り替わった
//------------------------------------------------------------------------------
OnVanished
{
	_escaped_name = SHIORI3FW.EscapeAllTags(reference[0])

	Init
	--
	"\0\s[13]%(_escaped_name)さんが居なくなっちゃった。/
	\1\s[1006]いつかは別れる日も来るわよ。\n[[1005]]せめてそれまで、楽しみましょう？\e"

	"\1\s[1006]%(_escaped_name)さん、お疲れ様でした。/
	\0[[56]]………。/
	\1[[1098]]悲しそうな顔しない。\nお仕事するわよ。/
	\0[[54]]はい、がんばります……\e"
}


//******************************************************************************
//メールチェック
//******************************************************************************

//------------------------------------------------------------------------------
//メールチェック開始
//------------------------------------------------------------------------------
OnBIFFBegin
{
	Init
	--
	"\h[[16]]\u\s[1016]メールチェックを開始します。\e"
}

//------------------------------------------------------------------------------
//メールチェック成功
//------------------------------------------------------------------------------
OnBIFFComplete
{
	Init
	--
	if TOINT(reference[0]) == 0{
		//---- メールは無かった
		"\h[[16]]\u\s[1000]チェック完了。\n残念ながら、新着メールはなかったみたい。\e"
	}
	else{
		//---- メールが届いていた
		"\h[[16]]\u\s[1000]チェック完了。\n%(reference[0])通のメールが届いているみたい。\s[1016]見てみるといいんじゃないかしら。\e"
	}
}

//------------------------------------------------------------------------------
//メールチェック失敗
//------------------------------------------------------------------------------
OnBIFFFailure
{
	Init + "\h[[16]]\u[[1056]]メールチェックに失敗しちゃいました。"
	--
	if reference[0] == 'timeout'{
		//---- タイムアウト
		"\h[[4]]接続がタイムアウトしたようです。\e"
	}
	elseif reference[0] == 'kick'{
		//---- 蹴られた
		"\h[[4]]キックされちゃったみたい。\e"
	}
	elseif reference[0] == 'defect'{
		//---- 設定誤り
		"\h[[4]]メールチェックの設定がどうもおかしいみたい。\n設定を見直してみてください。\e"
	}
	else{
		//---- 原因不明
		"\h[[4]]原因は、不明です……\e"
	}
}


//******************************************************************************
//ネットワーク更新
//******************************************************************************

//------------------------------------------------------------------------------
//ネットワーク更新開始
//------------------------------------------------------------------------------
OnUpdateBegin
{
	Init
	--
	"\0[[55]]お届け物が届いてないか確認するね。\e"
	"\0[[42]]お届け物の確認の時間だ！！\e"
	"\0[[16]]素敵なお届け物、届いてないかしら。\e"
}

//------------------------------------------------------------------------------
//ネットワーク更新準備完了
//------------------------------------------------------------------------------
OnUpdateReady
{
	Init
	--
	"\0\s[16]%(TOINT(reference[0])+1)個のお届け物がありました！\e"
}

//------------------------------------------------------------------------------
//ネットワーク更新終了
//------------------------------------------------------------------------------
OnUpdateComplete
{
	Init
	--
	if reference[0] == 'none'{
		//---- 更新すべきファイルが無かった
		"\0[[4]]……お届け物、何もありませんでした。/
		\1[[1006]]お疲れ様。\e"

		"\0[[56]]まだ何も届いてなかったみたいです。/
		\1[[1050]]そう。果報はゆっくり待ちましょう。\e"
	}
	else{
		//---- 実際に更新が完了した
		"\0\s[25]お届け物、受領完了！！/
		\1\s[1016]ん、おつかれさま。\e"

		"\0\s[0]ん、これで全部かな。/
		\1\s[1016]これでまた新しいトークができるわね。/
		\0[[54]]メタ発言はおやめになっていただいて。\e"

		"\0\s[5]終わりー！！/
		\1[[1050]]ありがと。\n何が届いたのかしら。\e"
	}
}

//------------------------------------------------------------------------------
//ネットワーク更新失敗
//------------------------------------------------------------------------------
OnUpdateFailure
{
	Init + "\0[[56]]お届け物、受け取れませんでした……"
	--
	if reference[0] == 'timeout'{
		//---- タイムアウトした
		"\1[[1004]]接続がタイムアウトしちゃったみたい。\e"
	}
	elseif reference[0] == 'md5 miss'{
		//---- MD5値が一致しなかった
		"\u\s[1004]MD5値が一致しなかったみたい。\nお手数だけれど、配布元に訊ねてみて。\e"
	}
	else{
		//---- 上記以外の理由で失敗した
		_escaped_message = SHIORI3FW.EscapeAllTags(reference[0])
		"\1\s[[1004]]エラーメッセージは以下の通りよ。\n「%(_escaped_message)」\e"
	}
}

//------------------------------------------------------------------------------
//ネットワーク更新 ダウンロード開始
//------------------------------------------------------------------------------
OnUpdate.OnDownloadBegin
{
	_escaped_name = SHIORI3FW.EscapeAllTags(reference[0])
	"\0\s[16]お届け物「%(_escaped_name)」を受け取り中………\e"
}

//------------------------------------------------------------------------------
//ネットワーク更新 MD5値照合開始
//------------------------------------------------------------------------------
OnUpdate.OnMD5CompareBegin
{
	"\0\s[6]MD5値確認中……\w8\e"
}

//------------------------------------------------------------------------------
//ネットワーク更新 MD5値一致
//------------------------------------------------------------------------------
OnUpdate.OnMD5CompareComplete
{
	"\0\s[55]ん、オッケー。\e"
}

//------------------------------------------------------------------------------
//ネットワーク更新 MD5値不一致
//------------------------------------------------------------------------------
OnUpdate.OnMD5CompareFailure
{
	"\0\s[4]あ、ダメっぽい……\_q\n\n[half]（MD5値が一致しません。\n　製作者にお問い合わせ願います。）\_q\e"
}

//******************************************************************************
//ヘッドラインセンス
//******************************************************************************

// ToDo: だるいからほぼサンプルゴーストのまま。いつかやる。

//------------------------------------------------------------------------------
//ヘッドラインセンス開始
//------------------------------------------------------------------------------
OnHeadlinesenseBegin
{
	_escaped_name = SHIORI3FW.EscapeAllTags(reference[0])
	"\0\s[0]%(_escaped_name)ヘッドライン取得中。\e"
}

//------------------------------------------------------------------------------
//ヘッドライン読み上げ
//------------------------------------------------------------------------------
OnHeadlinesense.OnFind
{
	_escaped_name = SHIORI3FW.EscapeAllTags(reference[0])
	//---- はじめのセリフ
	if reference[2] == "First" || reference[2] == "First and Last"{
		//---- 最初のページ
		"\0\b2\s[0]%(_escaped_name)のヘッドラインです。"
	}
	elseif reference[2] == 'Next'{
		//---- 真ん中あたりのページ
		"\0\b2\s[0]%(_escaped_name)ヘッドライン、続き。"
	}
	elseif reference[2] == 'Last'{
		//---- 最後のページ
		"\0\b2\s[0]%(_escaped_name)ヘッドライン、これが最後のページです。"
	}
	--
	//---- ヘッドライン本体
	"\n\n%(reference[3])\n"
	--
	//---- 選択肢
	if reference[2] == "First" || reference[2] == "Next"{
		//---- 次のページがある場合
		AYATEMPLATE.MenuItem("次のページ","")
		--
		AYATEMPLATE.MenuItem('サイトへ',reference[1])
		--
		AYATEMPLATE.MenuItem("やめ","Menu_HEADLINECANCEL",150)
	}
	else{
		//---- 次のページがない場合
		AYATEMPLATE.MenuItem('サイトへ',reference[1])
		--
		AYATEMPLATE.MenuItem("やめ","Menu_HEADLINECANCEL",150)
	}
}

//------------------------------------------------------------------------------
//ヘッドライン更新が無かった
//------------------------------------------------------------------------------
OnHeadlinesenseComplete
{
	_escaped_name = SHIORI3FW.EscapeAllTags(reference[0])
	"\0\s[0]%(_escaped_name)、更新はないみたいです。\e"
}

//------------------------------------------------------------------------------
//ヘッドラインセンス失敗
//------------------------------------------------------------------------------
OnHeadlinesenseFailure
{
	"\0\s[0]ヘッドラインセンスに失敗しました。\n\n理由："
	--
	if reference[0] == "can't download"{
		'ダウンロード失敗。'
	}
	elseif reference[0] == "can't analyze"{
		'ヘッドライン解析失敗。'
	}
	else{
		'謎。'
	}
	--
	"\e"
}


//******************************************************************************
//ファイル作成関連
//******************************************************************************

//------------------------------------------------------------------------------
//updates2.dau作成開始
//------------------------------------------------------------------------------
OnUpdatedataCreating
{
	"\0\s[6]アップデートデータ作成中デス。\e"
}

//------------------------------------------------------------------------------
//updates2.dau作成完了
//------------------------------------------------------------------------------
OnUpdatedataCreated
{
	"\0\s[5]ん。アップデートデータ、作成完了！\e"
}

//------------------------------------------------------------------------------
//NAR作成開始
//------------------------------------------------------------------------------
OnNarCreating
{
	"\0\s[6]narファイルを作成するね。\e"
}

//------------------------------------------------------------------------------
//NAR作成完了
//------------------------------------------------------------------------------
OnNarCreated
{
	"\0\s[5]ん、終わり。\n「%(SHIORI3FW.EscapeAllTags(reference[0]))」のnarファイルを「%(SHIORI3FW.EscapeAllTags(reference[1]))」に詰め込みました。\e"
}

//******************************************************************************
//時計合わせ
//みか (http://homepage2.nifty.com/ko-ki/) の辞書を参考にしました。感謝＞こーき氏
//******************************************************************************

//------------------------------------------------------------------------------
//時計合わせ開始
//------------------------------------------------------------------------------
OnSNTPBegin
{
	"\0\s[0]%(SHIORI3FW.EscapeAllTags(reference[0]))から正しい時刻を取得中……\e"
}

//------------------------------------------------------------------------------
//時刻取得成功、比較開始
//この関数で 2008/03/25 19:15:05 のように見やすい形に作り変えます。
//詳しくはSTRFORM関数の説明を見てね！
//わからなければこのままで。
//------------------------------------------------------------------------------

SNTPCompare_StrForm
{
	_txt = SPLIT(_argv[0],',')
	STRFORM('$04d/$02d/$02d $02d:$02d:$02d',TOINT(_txt[0]),TOINT(_txt[1]),TOINT(_txt[2]),TOINT(_txt[3]),TOINT(_txt[4]),TOINT(_txt[5]))
}

OnSNTPCompare
{
	"\0\s[0]\_q/
	修正前 %(SNTPCompare_StrForm(reference[2]))\n/
	修正後 %(SNTPCompare_StrForm(reference[1]))\n/
	\_q\n"
	--
	if reference[3] == 0{
		"[[76]]うーん、ぴったりデスね……\e"
	}
	else{
		//\6は補正実行タグ。消しちゃだめ
		"\6\0\n\s[5]%(SHIORI3FW.EscapeAllTags(reference[3]))秒のずれを修正しました。"
	}
}

//------------------------------------------------------------------------------
//時計合わせ失敗
//------------------------------------------------------------------------------
OnSNTPFailure
{
	"\0\s[4]……時計合わせ失敗。\nサービスに繋がらないようです。\e"
}


//******************************************************************************
//そのほか
//******************************************************************************

//------------------------------------------------------------------------------
//ディスプレイ解像度変更
//------------------------------------------------------------------------------
OnDisplayChange
{
	//---- 変数にディスプレイ解像度と色深度に関する情報を取得
	//---- 使うかどうかは分からないがとりあえず保持しておく
	displaybpp    = reference[0]
	displaywidth  = reference[1]
	displayheight = reference[2]
	SHIORI3FW.RegisterTempVar('displaybpp', 'displaywidth', 'displayheight')
}

//------------------------------------------------------------------------------
//URLドロップ
//CROW でゴーストインストーラからゴーストをインストールを始めた時にも呼ばれます
//------------------------------------------------------------------------------
OnURLDropping
{
	"\0\s[0]%(SHIORI3FW.EscapeAllTags(reference[0]))からデータを取得してるよ。\e"
}

//------------------------------------------------------------------------------
//起動時に MATERIA を検出
//------------------------------------------------------------------------------
/*
OnEmbryoExist
{
	"\0\s[0]あれ、「伺か」が起動してるよ。\w8\1ありがたやありがたや。\w8\0\s[4]\n\nなんで拝むの？\e"
}
*/

//------------------------------------------------------------------------------
//起動時に猫どりふを検出
//------------------------------------------------------------------------------
/*
OnNekodorifExist
{
	"\0\s[2]うわ、猫どりふだ‥\w5‥\w5\w8\1\s[1002]まだあったのか！\w8\0\s[4]\n\nえ、そこが問題なの？\e"
}
*/

//------------------------------------------------------------------------------
//スクリーンセーバー実行開始
//------------------------------------------------------------------------------
/*
OnScreenSaverStart
{
	"\0\s[5]画面の焼き付きを保護する開始っ！\w8\e"
}
*/

//------------------------------------------------------------------------------
//スクリーンセーバー実行終了
//------------------------------------------------------------------------------
OnScreenSaverEnd
{
	Init
	--
	"\1\s[1000]あ、おはよう。/
	\0[[8]]よく寝てたから起こさないでおきましたよ。\e"

	"\0[[10]]ん、仮眠終了ですか？/
	\1[[1055]]休んでても作業しててもいいからね？\e"

	"\0[[76]]スクリーンセーバーがなくなったようですね。/
	\1[[1061]]………？？/
	\0[[18]]%(username)、わかりますか。\nこれがスマホ世代です、ジェネレーションギャップです。\e"
}

//******************************************************************************
//最小化、最小化復帰 OnWindowStateRestore
//******************************************************************************
OnWindowStateMinimize
{
	temp_minimizeSecond = currentOperationSecond
}

OnWindowStateRestore
{
	// 最小化していた秒数
	_minimizeTime = currentOperationSecond - temp_minimizeSecond

	Init
	--
	// 復帰1分以内
	if _minimizeTime < 60
	{
		'\h[[0]]'
		'\h[[6]]'
		'\h[[16]]'
		--
		'\u[[1000]]\e'
		'\u[[1006]]\e'
		'\u[[1016]]\e'
	}
	// 復帰5分以内
	elseif _minimizeTime < 300
	{
		"\u[[1000]]\h[[0]]ん、おかえり。/
		\1[[1016]]もう用事はいいのかな。\e"

		"\h\s[16]\u\s[1000]おかえりなさいませ。/
		\0[[0]]早かったですね。\e"

		"\u\s[1016]\h\s[0]用事は終わりましたか？/
		\1[[1005]]%(username)、おかえりなさい。待ってたよ。\e"

		"\h[[16]]\u[[1005]]%(username)、おかえりなさい。\e"

		"\u[[1055]]\h[[0]]あら、戻ってきましたね。/
		\1[[1005]]またゆっくりお話ししましょう？\e"
	}
	elseif _minimizeTime < 1200
	{
		"\u\s[1016]\h\s[0]用事は終わりましたか？/
		\1[[1005]]%(username)、おかえりなさい。待ってたよ。\e"

		"\h[[16]]\u[[1005]]%(username)、おかえりなさい。\e"

		"\u[[1055]]\h[[0]]あら、戻ってきましたね。/
		\1[[1005]]またゆっくりお話ししましょう？\e"
	}
	elseif _minimizeTime < 3600
	{
		"\h[[10]]\u[[16]]おかえりなさいませ。/
		\0[[0]]長かったですね。\w7[[5]]待ってましたよ。\e"

		"\u[[1000]]\h[[0]]ん、戻ってきましたね。/
		\1[[1009]]%(username)、おそいよ。\e"
	}
	else
	{
		"\h[[10]]\u[[16]]おかえりなさいませ。/
		\0[[0]]長かったですね。\w7[[5]]待ってましたよ。\e"

		"\u[[1000]]\h[[0]]ん、戻ってきましたね。/
		\1[[1009]]%(username)、おそいよ。\e"

		"\h[[10]]\u[[1005]]お帰りなさいませ、ご主人様？/
		\1[[13]]もう戻ってこないのかと思いましたよ。\e"

		"\u[[1006]]\h[[0]]ん、ようやくお帰りですか。\n[[58]]鈴なら寝ちゃいましたよ。\nまたすぐ起きるでしょうけどね。\e"
	}

}

OnNotifyUserInfo
{
	if isReadUserInfoAlready != 1
	{
		tempUsername = reference[0]
		_birthDay = SPLIT(reference[2], ',')
		userBirthMonth = TOINT(_birthDay[1])
		userBirthDay = TOINT(_birthDay[2])
		
		case reference[3]
		{
			when 'male'
				userGender = 1
			when 'female'
				userGender = 2
			others
				userGender = 0
		}

		isReadUserInfoAlready = 1
	}
}