//******************************************************************************
// 食べ物や飲み物を注文したりゴーストに差し入れする機能
//******************************************************************************

// 自動でトーク中の改行にウェイトを追加
// #define \n[half] %(NewLine(1))
// #define \n %(NewLine(2))

// 最初のスコープ変更以外に改行とウェイトを追加
#define '\0 '\h
#define '\1 '\u
#define "\0 "\h
#define "\1 "\u
#define \0 %(NewLine(3))\0
#define \1 %(NewLine(3))\1

// 鈴の一人称を書き換え
#define わたし %(GetSuzuMyself)

// サーフェス短縮表記
#define [[ \s[
#define ]] ]

//******************************************************************************
// フードオーダー周り
//******************************************************************************

OpenFoodMenu
{
    Init
    --
    "\1はいっ。%(NewLine(2))こちらフードメニューになります。"
    --
    if month == userBirthMonth && day == userBirthDay && !isReceivedBirthDayParfait{
        '\_q\n\n[half]\![*]\q[スペシャル猫パフェ,ChooseFood,スペシャル猫パフェ]'
    }
    --
    AYATEMPLATE.MenuItem('ドリンク', 'DrinkMenu', 150)
    --
    AYATEMPLATE.MenuItem('ホットドリンク', 'HotDrinkMenu')
    --
    AYATEMPLATE.MenuItem('アルコール', 'AlcoholMenu')
    --
    AYATEMPLATE.MenuItem('フードメニュー', 'FoodMenu')
    --
    AYATEMPLATE.MenuItem('スイーツ', 'SweetsMenu')
    --
    AYATEMPLATE.MenuItem('おまかせで', 'OmakaseMenu', 150)
    --
    AYATEMPLATE.MenuItem('やっぱりやめる', 'Menu_Close', 150)

}

ChooseFood
{
    temp_chosenFood = temp_ChoiseExRef

    Init
    --
    "\2\_q【%(temp_chosenFood)】\_q"
    --
    GetFoodsData(temp_chosenFood, FOOD_DESCRIPTION)
    --
    '\2'
    --
    AYATEMPLATE.MenuItem('オーダーする', 'OrderFood', 150)
    --
    AYATEMPLATE.MenuItem('他のメニューも見てみる', 'OpenFoodMenu')
}

OrderFood
{
    Init
    --
    "\u[[1016]]\h[[16]]はーい。それじゃ、作ってくるね。[[999]]/
    \0…\w9\w9\w9…\w9\w9\w9…/
    \0[[16]]おまたせしました。%(NewLine(2))/
    %(temp_chosenFood)になります。\2"
    --
    AYATEMPLATE.MenuItem('いただく', 'EatFood', 0)
    --
    AYATEMPLATE.MenuItem('悠梨に差し入れる', 'GiveToYuri', 150)
    --
    AYATEMPLATE.MenuItem('鈴に差し入れる', 'GiveToSuzu')
}

EatFood
{
    Init
    --
    GetFoodsData(temp_chosenFood, FOOD_TEXT_TASTING)
    --
    '\e'
}

GiveToYuri
{
    fav_Yuri += GetFoodsData(temp_chosenFood, FOOD_GET_FAV_YURI)

    Init
    --
    GetFoodsData(temp_chosenFood, FOOD_TEXT_TO_GIVE_YURI)
    --
    '\e'
}

GiveToSuzu
{
    fav_Suzu += GetFoodsData(temp_chosenFood, FOOD_GET_FAV_SUZU)

    Init
    --
    GetFoodsData(temp_chosenFood, FOOD_TEXT_TO_GIVE_SUZU)
    --
    '\e'
}

//******************************************************************************
// 各フードカテゴリーごとのテキスト
//******************************************************************************

DrinkMenu
{
    _arrLen = WD_drink(-1)
    _text = Init
    _text += '\1\_q ---- ドリンクメニュー ----\n[half]'
    
    for _i = 0; _i < _arrLen; _i++ {
        _text += "\n\![*]\q[%(WD_drink(_i)),ChooseFood,%(WD_drink(_i))]"
    }

    _text += '\_q'

    _text
    --
    AYATEMPLATE.MenuItem('もどる', 'OpenFoodMenu', 150)
    --
    AYATEMPLATE.MenuItem('やっぱりやめる', 'Menu_Close')
}

HotDrinkMenu
{
    _arrLen = WD_hotdrink(-1)
    _text = Init
    _text += '\1\_q ---- ホットドリンク ----\n[half]'
    
    for _i = 0; _i < _arrLen; _i++ {
        _text += "\n\![*]\q[%(WD_hotdrink(_i)),ChooseFood,%(WD_hotdrink(_i))]"
    }

    _text += '\_q'

    _text
    --
    AYATEMPLATE.MenuItem('もどる', 'OpenFoodMenu', 150)
    --
    AYATEMPLATE.MenuItem('やっぱりやめる', 'Menu_Close')
}

AlcoholMenu
{
    _arrLen = WD_alcohol(-1)
    _text = Init
    _text += '\1\_q ---- アルコール ----\n[half]'
    
    for _i = 0; _i < _arrLen; _i++ {
        _text += "\n\![*]\q[%(WD_alcohol(_i)),ChooseFood,%(WD_alcohol(_i))]"
    }

    _text += '\_q'

    _text
    --
    AYATEMPLATE.MenuItem('もどる', 'OpenFoodMenu', 150)
    --
    AYATEMPLATE.MenuItem('やっぱりやめる', 'Menu_Close')
}

FoodMenu
{
    _arrLen = WD_food(-1)
    _text = Init
    _text += '\1\_q ---- フードメニュー ----\n[half]'
    
    for _i = 0; _i < _arrLen; _i++ {
        _text += "\n\![*]\q[%(WD_food(_i)),ChooseFood,%(WD_food(_i))]"
    }

    _text += '\_q'

    _text
    --
    AYATEMPLATE.MenuItem('もどる', 'OpenFoodMenu', 150)
    --
    AYATEMPLATE.MenuItem('やっぱりやめる', 'Menu_Close')
}

SweetsMenu
{
    _arrLen = WD_sweets(-1)
    _text = Init
    _text += "\1\_q ---- スイーツ ----\n\n[half]今月の限定メニュー\n\![*]\q[%(WD_newmenu),ChooseFood,%(WD_newmenu)]\n[half]"
    
    for _i = 0; _i < _arrLen; _i++ {
        _text += "\n\![*]\q[%(WD_sweets(_i)),ChooseFood,%(WD_sweets(_i))]"
    }

    _text += '\_q'

    _text
    --
    AYATEMPLATE.MenuItem('もどる', 'OpenFoodMenu', 150)
    --
    AYATEMPLATE.MenuItem('やっぱりやめる', 'Menu_Close')
}

OmakaseMenu
{
    // メニューの総数をカウント
    _menuCount = WD_drink(-1) + WD_hotdrink(-1) + WD_alcohol(-1) + WD_food(-1) + WD_sweets(-1) + 1

    _randomMenu = RAND(_menuCount)

    if _randomMenu < WD_drink(-1)
    {
        temp_ChoiseExRef = WD_drink(_randomMenu)
    }
    else{
        _randomMenu -= WD_drink(-1)

        if _randomMenu < WD_hotdrink(-1)
        {
            temp_ChoiseExRef = WD_hotdrink(_randomMenu)
        }
        else{
            _randomMenu -= WD_hotdrink(-1)

            if _randomMenu < WD_alcohol(-1)
            {
                temp_ChoiseExRef = WD_alcohol(_randomMenu)
            }
            else{
                _randomMenu -= WD_alcohol(-1)

                if _randomMenu < WD_food(-1)
                {
                    temp_ChoiseExRef = WD_food(_randomMenu)
                }
                else{
                    _randomMenu -= WD_food(-1)

                    if _randomMenu < WD_sweets(-1)
                    {
                        temp_ChoiseExRef = WD_sweets(_randomMenu)
                    }
                }
            }
        }
    }

    ChooseFood
}